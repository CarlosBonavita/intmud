classe jogoini
# Inicializa as seguintes classes:
# jogoserv - jogar em uma porta
# jogolocal - jogar localmente
# jogobot - jogar em um chat
telatxt tela
intexec fimprog # Para fechar o programa
debug dbg # Para informar sobre erros no programa
const iniclasse = !$[arg0] && criar(arg0)

func ini
  int8 x
  epara x = 1, x <= 10, x += 1
    refvar lin = config:inicom[x]
    casovar txtmin(txt1(lin))
    casose "bot"
      sair inttxtsub(lin) < 5
      criar("jogobot", txt2(lin))
      sair
    casose "telnet"
    casose "papo"
    casose "ssl"
      sair intsub(lin) < 3
      criar("jogoserv", lin)
      sair
    casofim
  efim
  config:modolocal && criar("jogolocal")

func tela_tecla
  se arg0 == "F10" # Recarregar o programa
    $mudaprog.exec
    ret 1
  senao arg0 == "ESC" # Encerrar o programa
    fimprog = 1
    $miscsav.salvartudo
    ret 1
  fimse

func fimprog_exec
  terminar

func dbg_erro
# tela.msg("\b\d1Erro:   " + txtsublin(arg0, 0, 1) + "\b\n" + txtsublin(arg0, 1, 10))
  indiceitem item
  epara item.ini("un "), txt1(item.txt) == "un", item.depois
    continuar !item.obj.jogconfig.15
    refvar t = item.obj.txtmais
    t.limpar
    t.addfim(arg0)
    t.addini("\b\d1(EXECLOG) " + t.remove + "\b")
    t.dividelin(75, 75)
    item.obj.txtmostra
  efim
