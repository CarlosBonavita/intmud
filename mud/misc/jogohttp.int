classe jogohttp
# HTTP rodando independente
serv servidor # Para receber conexões
indiceobj conexao1 # "cn " + porta
txt100 conexao2 # Texto comando COM: mesmo que arg0 na função ini

func ini
# arg0 contém 3 textos separados por espaço: tipo, endereço, porta
# Tipo deve ser "http"
# Se o endereço for "*", abre a porta em todos os endereços
  conexao1 = "cn " + txtsub(arg0, 2)
  conexao2 = arg0
  refvar ender = txtsub(arg0, 1, 1)
  refvar t1 = servidor.abrir(ender == "*" ? "" : ender, txtsub(arg0, 2))
  se t1 != "1"
    msg("Não foi possível abrir a porta")
    apagar(este), conexao2 = ""
    ret
  fimse
  msg("Esperando conexões")

func fim
  conexao2 && msg("Conexão encerrada")

func msg
  se !config:modolocal
    telatxt tela
    tela.msg("Serv " + conexao2 + ": " + arg0 + "\n")
  fimse
  indiceitem item
  refvar m = "\b\d1(com) " + conexao2 + ": " + arg0 + "\b"
  epara item.ini("un "), txt1(item.txt) == "un", item.depois
    item.obj.jogconfig.21 && item.obj.msg(m)
  efim

func servidor_socket # Conectado: cria objeto
  criar("jogohttp_ini", arg0, este)


classe jogohttp_ini
socket conec # Usuário conectado
inttempo tempo # Para encerrar conexão após um tempo
uint8 passo # 0=definindo se Papovox, 1=entrando Papovox, 2=dentro
uint8 penv # Para enviar página HTML
txt100 host # Conteúdo da linha Host
txt100 origin # Conteúdo da linha Origin
txt100 key

func ini
# arg0 = socket
# arg1 = objeto jogohttp
  conec = arg0
  tempo = 80

func tempo_exec
  apagar(este)

func conec_msg
# telatxt t
# t.msg(">" + passo + "/" + arg0 + "\n")
  casovar passo
  casose "0" # Primeira linha do cabeçalho
    se txt1(arg0) != "GET"
      apagar(este)
    senao txtsub(arg0, 1, 1) == "/ws"
      passo = 2
    senao txtsub(arg0, 1, 1) != "/"
      conec.msg("HTTP/1.0 404 NOT FOUND\n\n")
    senao
      passo = 1
    fimse
    ret
  casose "1" # Página HTML: esperando fim do cabeçalho
    se txt1(arg0) == "host:"
      host = txt2(arg0)
    senao txt1(arg0) == "origin:"
      origin = txt2(arg0)
    senao arg0 != ""
    senao host == ""
      apagar(este)
    senao
      tempo = 100
      passo = 10
      conec_env
    fimse
    ret
  casose "2" # WebSocket
  casose "3"
    se txt1(arg0) == "host:"
      host = txt2(arg0)
    senao txt1(arg0) == "origin:"
      origin = txt2(arg0)
    senao txt1(arg0) == "Sec-WebSocket-Key:"
      key = txt2(arg0)
    senao txt1(arg0) == "Upgrade:" && txt2(arg0) == "WebSocket"
      passo = 3
    senao arg0 != ""
    senao host == "" || key == "" || passo != 3
      apagar(este)
    senao
      txt(origin, 0, 7) == "http://" && (origin = txt(origin, 7))
      txt(host, 0, 7) == "http://" && (host = txt(host, 7))
      ret origin != host, apagar(este) # Somente conexões do mesmo servidor
      refvar t1 = txtconv(txtsha1(key + "258EAFA5-E914-47DA-95CA-C5AB0DC85B11"), "hb")
# telatxt t
# t.msg("KEY  [" + key + "]\nRESP [" + t1 + "]\n")
      conec.msg("HTTP/1.1 101 Switching Protocols\n")
      conec.msg("Upgrade: websocket\n")
      conec.msg("Connection: Upgrade\n")
      conec.msg("Sec-WebSocket-Accept: " + t1 + "\n\n")
      conec.proto = 6
# conec.msg("Sec-WebSocket-Protocol: chat\n\n")
# Sec-WebSocket-Origin: http://" + host + "\n")
# conec.msg("Sec-WebSocket-Location: ws://" + host + "\n")
      apagar(este)
      criar("jogohttp_usr", conec)
    fimse
    ret
  casose "10"
    ret
  casose
    apagar(este)
    ret
  casofim

func conec_env
  se passo == 4
  senao !msg_[passo]
    apagar(este)
  senao conec.msg(txtconv(msg_[passo], "tu"))
    passo += 1
  fimse

func fim
# telatxt t
# t.msg(">FECHOU\n")

const msg_10 = "HTTP/1.0 200 OK\n\
Server: MUD\n\
Pragma: no-cache\n\
Cache-Control: max-age=0\n\
Expires: Thu, 01 Jan 1970 00:00:00 GMT\n\
Content-Type: text/html; charset=utf-8\n\n"
#
const msg_11 = "<!DOCTYPE html>\n\
<html style=\"padding: 5px;\" lang=\"en\"><head>\n\
<meta http-equiv=\"content-type\" content=\"text/html; charset=windows-1252\">\n\
  <meta charset=\"ISO-8859-1\">\n\
  <style type=\"text/css\">\n\
  <!--\n\
    body {\n\
      font-family: Monaco,Consolas,Lucida Console,DejaVu Sans Mono,Courier New,Droid Sans \
Mono,monospace;\n\
      background-color: #1D1D1D;\n\
      color: #C0C0C0;\n\
      height: 97%;\n\
      width: 100%;\n\
      min-width: 700px;\n\
      margin: 0;\n\
      padding: 0;\n\
      font-size: 85%;\n\
    }\n\
    * {\n\
      font-family: Monaco,Consolas,Lucida Console,DejaVu Sans Mono,Courier New,Droid Sans \
Mono,monospace;\n\
      font-size: 100%;\n\
    }\n\
    #txt {\n\n\
    }\n\
/*    input {\n\
      background: 0; border: 0; color: #C0C0C0; outline: 0;\n\
    }*/\n\
  -->\n\
  </style>\n"
#
const msg_12 = "</head>\n\
<body>\n\
  <noscript style=\"color: #ff0000;\">Please enable Javascript.</noscript>\n\
  <!-- div style=\"position: fixed; height: auto; width: 99%; margin: 0 auto; top: 0; bottom: \
30px; overflow: auto;\"> -->\n\
  <div style=\"height: auto; width: auto; margin: 0 0 40px 0;\">\n\
    <pre id=\"terminal\" style=\"height: auto; width: 100%; margin: 0 auto;\"></pre>\n\
<!-- aa<span style=\"color:#FF8000;background-color:#000080;\">b</span>c -->\n\
  </div>\n\
  <div style=\"position: fixed; top: auto; bottom: 0; left: 0; right: 0; height: 40px; background\
-color:#1D1D1D;\">\n\
    <input size=\"77\" name=\"i\" id=\"cmd\" type=\"text\" style=\"margin: 11px 2px;\">\n\
    <div style=\"margin: 0em\">\n\
    <img id=\"vida\" src=\"data:image/gif;base64,R0lGODlhCgAFAIACAAAAAM4yMiH+EUNy\n\
ZWF0ZWQgd2l0aCBHSU1QACwAAAAACgAFAAACCYSPocvtr5AEBQA7\"\n\
width=\"2px\" height=\"5px\" style=\"left:10px; top:2px; position: absolute;\">\n\
    <img id=\"mana\" src=\"data:image/gif;base64,R0lGODlhCgAFAIACAAAAADLONiwAAAAA\n\
CgAFAAACCYSPocvtr5AEBQA7\"\n\
width=\"2px\" height=\"5px\" style=\"left:220px; top:2px; position: absolute;\">\n\
    <img id=\"move\" src=\"data:image/gif;base64,R0lGODlhCgAFAIACAAAAADIyziwAAAAA\n\
CgAFAAACCYSPocvtr5AEBQA7\"\n\
width=\"2px\" height=\"5px\" style=\"left:430px; top:2px; position: absolute;\">\n\
    </div>\n\
    <img id=\"exp\" src=\"data:image/gif;base64,R0lGODlhCgADAIABAJ6env///ywAAAAA\n\
CgADAAACBYSPqWsFADs=\"\n\
width=\"2px\" height=\"2px\" style=\"left:10px; top:7px; position: absolute;\">\n\
  </div>\n"
#
const msg_13 = "<script><!--\n\n\
var historico = [ ]; // Histórico do que foi digitado\n\
var histnum = 0; // Em qual linha do histórico está\n\
var conectado = false; // Se está conectado\n\
var conec = null; // Conexão WebSocket\n\n\
var corfrente = 7; // 0-15\n\
var corfundo = 0; // 0-15\n\
var coratrib = 0; // +1=sublinhado +2=piscante +4=troca frente/fundo\n\n\
// Add and remove events, based on\n\
// http://www.javascripter.net/faq/addeventlistenerattachevent.htm\n\
function addEventHandler(obj,eventType,handler) {\n\
  var elem = document.getElementById(obj);\n\
  if (elem.addEventListener) elem.addEventListener (eventType,handler,false);\n\
  else if (elem.attachEvent) elem.attachEvent ('on'+eventType,handler);\n\
}\n\n\
function removeEventHandler(obj,eventType,handler) {\n\
  var elem = document.getElementById(obj);\n\
  if (elem.removeEventListener) elem.removeEventListener (eventType,handler,false);\n\
  if (elem.detachEvent)         elem.detachEvent ('on'+eventType,handler);\n}\n\n"
#
const msg_14 = "// Adiciona texto no terminal\n\
function env_tela(msg) {\n\
  var term = document.getElementById('terminal');\n\
  while (term.childElementCount >= 100)\n\
    term.removeChild(term.firstChild);\n\
  var node = document.createElement(\"span\");\n\
  node.innerHTML = msg + \"\\n\";\n\
  term.appendChild(node);\n\
  window.scrollTo(0,document.body.scrollHeight);\n\
  return node;\n\
}\n\n\
// Retorna o código da cor a partir do número da cor, de 0 a 15\n\
function estilo_cor(cor) {\n\
  switch (cor)\n\
  {\n\
    case 0:  return \"#1D1D1D;\"; break; // BLACK\n\
    case 1:  return \"#B00000;\"; break; // RED\n\
    case 2:  return \"#00B000;\"; break; // GREEN\n\
    case 3:  return \"#B0B000;\"; break; // YELLOW\n\
    case 4:  return \"#0000B0;\"; break; // BLUE\n\
    case 5:  return \"#B000B0;\"; break; // PURPLE\n\
    case 6:  return \"#00B0B0;\"; break; // CYAN\n\
    case 7:  return \"#C0C0C0;\"; break; // WHITE\n\
    case 8:  return \"#808080;\"; break; // BLACK\n\
    case 9:  return \"#FF0000;\"; break; // RED\n\
    case 10: return \"#00FF00;\"; break; // GREEN\n\
    case 11: return \"#FFD700;\"; break; // YELLOW\n\
    case 12: return \"#0000FF;\"; break; // BLUE\n\
    case 13: return \"#FF00FF;\"; break; // PURPLE\n\
    case 14: return \"#00FFFF;\"; break; // CYAN\n\
    case 15: return \"#FFFFFF;\"; break; // WHITE\n\
  }\n\
  return \"#000000\";\n}\n\n"
#
const msg_15 = "// Retorna o estilo (um texto) a partir dos atributos de cor\n\
function estilo_atrib() {\n\
  var estilo = \"\";\n\
  if ((coratrib & 4) != 0) {\n\
    estilo += \"color:\" + estilo_cor(corfundo);\n\
    estilo += \"background-color:\" + estilo_cor(corfrente);\n\
  }\n\
  else {\n\
    estilo += \"color:\" + estilo_cor(corfrente);\n\
    estilo += \"background-color:\" + estilo_cor(corfundo);\n\
  }\n\
  if ((coratrib & 3) == 1)\n\
    estilo += \"text-decoration:underline;\";\n\
  else if ((coratrib & 3) == 2)\n\
    estilo += \"text-decoration:blink;\";\n\
  else if ((coratrib & 3) == 3)\n\
    estilo += \"text-decoration:underline blink;\";\n\
  return estilo;\n}\n\n"
#
const msg_16 = "// Chegou mensagem do servidor\n\
function recv_message(e) {\n\
  //console.log(\">> recv_message: \" + e.data.length + \" = \" + e.data);\n\
  if (e.data.charAt(0) == '+')\n\
    msg_tela(e.data.substring(1));\n\
  else if (e.data.charAt(0) == 's') {\n\
    var res = e.data.substring(1).split(' ');\n\
    if (res.length >= 4) {\n\
      document.getElementById('vida').width = 2 * res[0];\n\
      document.getElementById('mana').width = 2 * res[1];\n\
      document.getElementById('move').width = 2 * res[2];\n\
      document.getElementById('exp').width = 620 * res[3] / 100;\n\
    }\n\
  }\n\
}\n\n\
function msg_tela(str) {\n\
  var ret = \"\"; // Texto que será enviado para a tela\n\
  var tam = 0; // Quantos caracteres na tela\n\
  var i = 0;\n\
  var est_ini = estilo_atrib();\n\
  var completa = (((coratrib) & 4) ? (corfrente != 0) : (corfundo != 0));\n\
  var estilo = \"\";\n\
  var atualizar = false;\n\
  var barra = false;\n\n"
#
const msg_17 = "  while (i < str.length)\n\
  {\n\
    if (str.charCodeAt(i) < 32 && str.charCodeAt(i) >= 0) {\n\
      i++;\n\
      continue;\n\
    }\n\
    var ch = str.charAt(i++);\n\
    if (ch != '\\\\' || barra) {\n\
      barra = false;\n\
      if (atualizar) {\n\
        atualizar = false;\n\
        var novo = estilo_atrib();\n\
        if (novo != estilo) {\n\
          if (estilo != \"\")\n\
            ret += \"</span>\";\n\
          estilo = novo;\n\
          if (estilo != \"\")\n\
            ret += \"<span style=\\\"\" + estilo + \"\\\">\";\n\
        }\n\
      }\n\
      if (ch == \"<\")\n\
        ret += \"&lt;\";\n\
      else if (ch == \">\")\n\
        ret += \"&gt;\";\n\
      else if (ch == \"&\")\n\
        ret += \"&amp;\";\n\
      else\n\
        ret += ch;\n\
      if (++tam >= 80) {\n\
        env_tela(ret).style = est_ini;\n\
        est_ini = estilo_atrib();\n\
        tam = 0;\n\
        ret = \"\";\n\
        completa = ((coratrib & 4 ? corfrente : corfundo) == 7);\n\
      }\n\
      continue;\n    }\n"
#
const msg_18 = "    if (i >= str.length)\n\
      break;\n\
    ch = str.charAt(i++);\n\
    if (ch == '\\\\' || ch == '\"') {\n\
      barra = true; i--;\n\
    }\n\
    else if (ch == 'b') {\n\
      corfrente = 7;\n\
      corfundo = 0;\n\
      coratrib = 0;\n\
      atualizar = true;\n\
    }\n\
    else if (ch == 'd') {\n\
      if (i >= str.length)\n\
        break;\n\
      switch (str.charAt(i++))\n\
      {\n\
      case '0': corfundo = 0; break;\n\
      case '1': corfundo = 1; break;\n\
      case '2': corfundo = 2; break;\n\
      case '3': corfundo = 3; break;\n\
      case '4': corfundo = 4; break;\n\
      case '5': corfundo = 5; break;\n\
      case '6': corfundo = 6; break;\n\
      case '7': corfundo = 7; break;\n\
      }\n\
      atualizar = true;\n    }\n"
#
const msg_19 = "    else if (ch == 'c') {\n\
      if (i >= str.length)\n\
        break;\n\
      switch (str.charAt(i++).toLocaleLowerCase())\n\
      {\n\
      case '0': corfrente = 0; break;\n\
      case '1': corfrente = 1; break;\n\
      case '2': corfrente = 2; break;\n\
      case '3': corfrente = 3; break;\n\
      case '4': corfrente = 4; break;\n\
      case '5': corfrente = 5; break;\n\
      case '6': corfrente = 6; break;\n\
      case '7': corfrente = 7; break;\n\
      case '8': corfrente = 8; break;\n\
      case '9': corfrente = 9; break;\n\
      case 'a': corfrente = 10; break;\n\
      case 'b': corfrente = 11; break;\n\
      case 'c': corfrente = 12; break;\n\
      case 'd': corfrente = 13; break;\n\
      case 'e': corfrente = 14; break;\n\
      case 'f': corfrente = 15; break;\n\
      case 'g': coratrib |=  1; break; // + sublinhado\n\
      case 'h': coratrib &= ~1; break; // - sublinhado\n\
      case 'i': coratrib |=  4; break; // + inverter frente/fundo\n\
      case 'j': coratrib &= ~4; break; // - inverter frente/fundo\n\
      case 'k': coratrib |=  2; break; // + piscante\n\
      case 'l': coratrib &= ~2; break; // - piscante\n\
      case 'm': // Echo off\n\
        document.getElementById('cmd').type = 'password';\n\
        break;\n\
      case 'n': // Echo on\n\
        document.getElementById('cmd').type = 'text';\n\
        break;\n\
      }\n\
      atualizar = true;\n\
    }\n\
  }\n\
  if (estilo != \"\")\n\
    ret += \"</span>\";\n\
  if (tam != 0) {\n\
    while (completa && tam < 80) {\n\
      ret += ' ';\n\
      tam++;\n\
    }\n\
    env_tela(ret).style = est_ini;\n\
  }\n}\n\n"
#
const msg_20 = "// Usuário pressionou uma tecla\n\
function tecla(e) {\n\
  var keysym = (e ? e : window.event).keyCode;\n\
  var cmd = document.getElementById('cmd');\n\
  //console.log(\"key \" + keysym);\n\n\
  switch (keysym)\n\
  {\n\
  case 38: // Seta para cima\n\
    if (histnum != 0)\n\
      cmd.value = historico[--histnum];\n\
    break;\n\
  case 40: // Seta para baixo\n\
    if (histnum + 1 == historico.length) {\n\
      cmd.value = \"\";\n\
      histnum++;\n\
    }\n\
    else if (histnum < historico.length)\n\
      cmd.value = historico[++histnum];\n\
    break;\n\
  case 13: // Enter\n\
    if (!conectado)\n\
      break;\n\
    //console.log(\"Enviar \" + cmd.value);\n\
    conec.send(\"+\" + cmd.value);\n\
    if (cmd.type == 'text') {\n\
      if (historico.length == 0)\n\
        historico.push(cmd.value)\n\
      else if (historico[historico.length - 1] != cmd.value)\n\
        historico.push(cmd.value)\n\
      if (historico.length > 100)\n\
        historico.shift();\n\
      histnum = historico.length;\n\
    }\n\
    cmd.value = \"\";\n\
    break;\n\
  }\n\n\
  return 0;\n}\n\n"
#
const msg_21 = "window.onload = function() {\n\
  document.getElementById(\"cmd\").focus();\n\
  addEventHandler(\"cmd\", \"keydown\", tecla);\n\
  if (window.WebSocket) {\n\
    msg_tela(\"Setas \\\\cbCIMA\\\\b e \\\\cbBAIXO\\\\b acessam o histórico do que você digitou\"\
);\n\
    env_tela(\"\");\n\
    conectar();\n\
  }\n\
  else {\n\
    env_tela(\"\\n\\nSeu navegador não suporta WebSocket;\\n\");\n\
    env_tela(\"Use um navegador mais recente\");\n\
    document.getElementById('cmd').style.display = 'none';\n\
  }\n}\n\n"
#
const msg_22 = "function conectar() {\n  var ender = '" + host + "';\n"
#
const msg_23 = "  msg_tela(\"Conectando em \" + ender);\n\
  conec = new WebSocket('ws://' + ender + '/ws');\n\
  conec.onmessage = recv_message;\n\
  conec.onopen = function() {\n\
    console.log(\">> WebSock.onopen\");\n\
    conectado = true;\n\
    msg_tela(\"Conectado!\");\n\
    //conec.send('Ping');\n\
  };\n\
  conec.onerror = function (error) {\n\
    console.log('WebSocket Error ' + error);\n\
    if (!conectado) {\n\
      msg_tela(\"\\\\cF\\\\d1Erro ao conectar\");\n\
      return;\n\
    }\n\
    conec.close();\n\
    conectado = false;\n\
    msg_tela(\"\\\\cF\\\\d1Desconectado\");\n\
  };\n\
  conec.onclose = function(error) {\n\
    console.log('WebSocket Error ' + error);\n\
    if (!conectado)\n\
      return;\n\
    conectado = false;\n\
    msg_tela(\"\\\\cF\\\\d1Desconectado\");\n\
  };\n\
}\n\
// -->\n\
</script>\n\
</body></html>\n"


classe jogohttp_usr
herda jogsock
socket j_conec # Usuário conectado
ref j_servip # Objeto jogoservip que possui esse objeto
# ref j_jogoserv # Objeto jogoserv
inttempo j_ping_tempo
inttempo j_pong_tempo
txt40 j_ping_txt # Mensagem enviada no ping
txt250 j_pong_txt # Mensagem pong que deve enviar
textotxt j_recebeu_txt # Parte da mensagem recebida
int8 infotelnet # 0 se Papovox (CRLF no final), 1 se Telnet (CRLF no começo), 2 se SSL
const msgsala = msgsock(arg0)
const info = "h " + j_conec.ip

func msgsock
  textotxt t
  t.addfim(txtconv(arg0, "tu"))
  enquanto t.linhas
    j_conec.msg(txtconv("+" + txtvis(t.remove), "th"), 129)
  efim

func msgtodos
  ref r
  epara r = $jogohttp_usr, r, r = objdepois(r)
    r.msgsock(arg0)
  efim

func msgprompt
  txt100 lin
  refvar r = perso.persobat
  ret !r, nulo
  lin = r.pvida >= r.pvidamax ? "100" : int(intpos(100 * r.pvida / r.pvidamax))
  lin += " " + (r.pmana >= r.pmanamax ? "100" : int(intpos(100 * r.pmana / r.pmanamax)))
  lin += " " + (r.pmove >= r.pmovemax ? "100" : int(intpos(100 * r.pmove / r.pmovemax)))
  refvar exp = r.expmax
  lin += " " + (r.expatual >= exp ? "100" : int(intpos(100 * r.expatual / exp)))
  j_conec.msg(txtconv("s" + lin, "th"), 129)

func salaobj
  ref r
  epara r = $jogoserv, r, r = objdepois(r)
    arg0.addfim(r)
  efim

func ini # Objeto foi criado
# arg0 = socket
# arg1 = objeto jogoserv
# Acerta variáveis
  j_conec = arg0
  j_conec.aflooder = 1
# Checa número de conexões por IP
  indiceitem i
  ref r
  r = i.obj("ip " + j_conec.ip)
  se !r
    r = criar("jogoservip")
    r.endereco = "ip " + j_conec.ip
  senao r.usr.total >= config:servusr
    msgsock("-Atingido limite de conexões por IP")
    apagar(este)
    ret
  senao r.entrou
    msgsock("-Entre daqui alguns segundos")
    apagar(este)
    ret
  fimse
  j_servip = r, r.usr.addfim(este)
  r.entrou = config:servtempo
  j_ping_tempo = 600
  jogsock:ini

func fim # Objeto foi apagado
  j_servip.usr.total <= 1 && j_servip.entrou <= 0 && apagar(j_servip)
  j_conec.msg(txtconv("Connection closed", "th"), 136)
  jogsock:fim

func j_ping_tempo_exec # Enviar ping de tempos em tempos
  se j_ping_txt
    j_conec.msg(txtconv("Ping timed out", "th"), 136)
    j_conec.fechar
    apagar(este)
  senao
    datahora dh
    dh.agora
    j_ping_tempo = 600
    j_ping_txt = txtconv(dh.numseg, "th")
    j_conec.msg(j_ping_txt, 137)
  fimse
# telatxt tt
# tt.msg("PING>" + j_ping_txt + "\n")

func j_pong_tempo_exec # Responder pong a um ping do navegador
  !j_conec.msg(j_pong_txt, 138) && (j_pong_tempo = 10)
# telatxt t
# t.msg("PONG>" + j_pong_txt + "\n")

func j_conec_fechou # Conexão fechou
  apagar(este)

func j_conec_msg # Recebeu mensagem do usuário
# arg0=mensagem
# telatxt tt
# tt.msg("<" + arg2 + "> " + txtconv(arg0, "ht") + "\n")
  casovar arg2
  casose "138" # Pong
    arg0 == j_ping_txt && (j_ping_txt = "")
    sair
  casose "137" # Ping
    j_pong_txt = arg0
    j_pong_tempo = 1
    sair
  casose "129" # Mensagem de texto
  casose "130" # Mensagem binária
    j_recebeu_txt.limpar
    refvar m1 = txtconv(txtconv(arg0, "ht"), "ut")
    txt(m1, 0, 1) == "+" && recebe(txtremove(txt(m1, 1), "ED"))
    sair
  casose "1" # Início de mensagem de texto
  casose "2" # Início de mensagem binária
    j_recebeu_txt.limpar
    j_recebeu_txt.addfim(arg0)
    sair
  casose "0" # Continuação da mensagem
    j_recebeu_txt.addfim(arg0)
    j_recebeu_txt.bytes >= 3000 && apagar(este)
    sair
  casose "128" # Última parte da mensagem
    refvar m1 = txtconv(txtconv(j_recebeu_txt.remove(1000) + arg0, "ht"), "ut")
    txt(m1, 0, 1) == "+" && recebe(txtremove(txt(m1, 1), "ED"))
    sair
  casose "136" # Fechar a conexão
    j_conec.msg(arg0, 136)
    j_conec.fechar
    apagar(este)
    sair
  casose # Código de mensagem desconhecido
    apagar(este)
    sair
  casofim
