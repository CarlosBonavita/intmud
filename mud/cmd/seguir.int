classe h_cmd_seguir
herda comando_comum
const classemenu = "m_cmd_seguir"
const posic = 5
#
const m_ajuda = "-t\n\
-s [personagem]\n\
Segue um personagem.\n\
Se o personagem for omitido ou for você mesmo, você passa a não seguir\n\
ninguém. Para outros personagens não seguirem você, veja a opção SEGUIR\n\
do comando CONFIG."
const m_naover = "Você não vê $m."
const m_temdono = "Você só quer seguir $a."
const m_ninguem = "Você não segue ninguém."
const m_seguir = "Você segue $a."
const m_naoseguir = "Você não consegue seguir $a."
const admordem = "m_ajuda m_naover m_temdono m_ninguem m_seguir m_naoseguir"

func escr
# Verifica se tem dono
  se arg0.tseguir == 3 && arg0.iseguir
    ret $mens.mp(m_temdono, arg0, r.iseguir.objlista)
# Sem argumentos: deixa de seguir
  senao !arg1
    arg0.iseguir.remove
    arg0.tseguir = 0
    $mensjog.msg(m_ninguem, arg0)
    ret
  fimse
# Procura o personagem
  $mens.mens = arg1
  listaobj l
  listaitem i
  epara i = arg0.dono.dentro2.ini, i, i.depois
    i.obj.perso && l.addfim(i.obj)
  efim
  epara i = arg0.dono.dentro1.ini, i, i.depois
    i.obj.i_seguir && l.addfim(i.obj)
  efim
  refvar r = misc:itemlista(arg0, arg1, l)
  ret !r, $mens.mp(m_naover, arg0)
  l.limpar
# Segue o personagem
  se r == arg0
    arg0.iseguir.remove
    arg0.tseguir = 0
    $mensjog.msg(m_ninguem, arg0)
  senao r.p_naoseguir(arg0)
    $mens.mp(m_naoseguir, arg0, r)
  senao
    arg0.seguir(r)
    $mens.mp(m_seguir, arg0, r)
  fimse


classe m_cmd_seguir
herda comando_menu_cmd
#
const nome1 = "Ajuda"
const tipo1 = "opc_texto"
const vari1 = "m_ajuda"
#
const nome2 = "Sem alvo"
const tipo2 = "opc_linha"
const vari2 = "m_naover"
#
const nome3 = "Tem dono"
const tipo3 = "opc_linha"
const vari3 = "m_temdono"
#
const nome3 = "Não segue ninguém"
const tipo3 = "opc_linha"
const vari3 = "m_ninguem"
#
const nome4 = "Segue alguém"
const tipo4 = "opc_linha"
const vari4 = "m_seguir"
#
const nome5 = "Não consegue seguir"
const tipo5 = "opc_linha"
const vari5 = "m_naoseguir"
