classe h_cmd_esvaziar
herda comando_comum
const txtajuda = txtajuda1 + m_ajuda
const txtajuda1 = "\b\c3Esvaziar\b\nSintaxe: ESVAZIAR <objeto>\n"
const posic = 5
#
const m_ajuda = "Tenta esvaziar o objeto, jogando fora o líquido que tiver dentro."
const m_sem_args = "Esvaziar o quê?"
const m_naover = "Você não vê $m."
const m_nao_consegue = "Você não consegue esvaziar $o."
const m_fechado = "$O está fechad$u."
const m_vazio = "$O está vazi$u."
const m_esvaziou1 = "Você esvaziou $o."
const m_esvaziou2 = "$P esvaziou $o."
const admordem = "m_ajuda m_sem_args m_naover m_nao_consegue m_fechado m_vazio\n\
m_esvaziou1 m_esvaziou2"

func escr
  listaobj l
# Obtém o objeto
  ret !arg1, $mensjog.msg(m_sem_args, arg0)
  refvar r1 = misc:itemdentro(arg0, txt1(arg1))
  se !r1
    $mens.mens = txt1(arg1)
    ret $mens.mp(m_naover, arg0)
  senao r1.i_aberto >= 3
    ret $mens.mp(m_fechado, arg0, nulo, r1)
  senao r1.f_tipo != 5
    ret $mens.mp(m_nao_consegue, arg0, nulo, r1)
  senao !r1.var.z_liquido
    ret $mens.mp(m_vazio, arg0, nulo, r1)
  fimse
# Eventos
  refvar liq = $liq_[txt2(r1.var.z_liquido)]
  ret liq.cmd_esvaziar(arg0, r1, liq, liq), 0
  epara l.addfim(arg0.evento), l, l.ini.remove
    ret l.objini.cmd_esvaziar(arg0, r1, liq, arg0), 0
  efim
  epara l.addfim(r1.evento), l, l.ini.remove
    ret l.objini.cmd_esvaziar(arg0, r1, liq, r1), 0
  efim
# Esvazia
  r1.var.z_liquido = ""
  r1.ajustapeso
  $mens.mens = liq.nome
  $mens.o_1 = liq.aspecto
  $mens.o_2 = liq.msexo ? "um" : "uma"
  $mens.o_3 = liq.msexo ? "o" : "a"
  $mens.p(arg0, nulo, r1)
  $mens.mtodos2(m_esvaziou1, m_esvaziou2)
# Eventos
  liq.cmd_esvaziou(arg0, r1, liq, liq)
  epara l.addfim(arg0.evento), l, l.ini.remove
    ret l.objini.cmd_esvaziou(arg0, r1, liq, arg0), 0
  efim
  epara l.addfim(r1.evento), l, l.ini.remove
    ret l.objini.cmd_esvaziou(arg0, r1, liq, r1), 0
  efim


classe m_cmd_esvaziar
herda comando_menu_cmd
#
const nome1 = "Ajuda"
const tipo1 = "opc_texto"
const clas1 = "cmd_esvaziar"
const vari1 = "m_ajuda"
#
const nome2 = "Sem argumentos"
const tipo2 = "opc_linha"
const clas2 = "cmd_esvaziar"
const vari2 = "m_sem_args"
#
const nome3 = "Sem alvo"
const tipo3 = "opc_linha"
const clas3 = "cmd_esvaziar"
const vari3 = "m_naover"
#
const nome4 = "Não consegue"
const tipo4 = "opc_linha"
const clas4 = "cmd_esvaziar"
const vari4 = "m_nao_consegue"
#
const nome5 = "Item fechado"
const tipo5 = "opc_linha"
const clas5 = "cmd_esvaziar"
const vari5 = "m_fechado"
#
const nome6 = "Item vazio"
const tipo6 = "opc_linha"
const clas6 = "cmd_esvaziar"
const vari6 = "m_vazio"
#
const nome7 = "Esvaziou perso"
const info7 = "$o=item esvaziado, $m=nome do líquido, $1=aspecto, $2=um ou uma, $3=o ou a"
const tipo7 = "opc_linha"
const clas7 = "cmd_esvaziar"
const vari7 = "m_esvaziou1"
#
const nome8 = "Esvaziou outros"
const info8 = "$o=item esvaziado, $m=nome do líquido, $1=aspecto, $2=um ou uma, $3=o ou a"
const tipo8 = "opc_linha"
const clas8 = "cmd_esvaziar"
const vari8 = "m_esvaziou2"
