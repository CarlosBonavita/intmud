classe h_cmd_reserva
herda comando_comum
const objcmd = config:animal1 && config:animal2 ? este
const txtajuda = config:animal1 && config:animal2 ? txtajuda1 + m_ajuda
const txtajuda1 = "\b\c3Reserva\b\n\
Sintaxe: RESERVA\n\
         RESERVA <nome>\n\
         RESERVA +<número>\n\
         RESERTA -<nome>\n"
#
const m_ajuda = "Sem argumentos mostra os seus animais na reserva.\n\
Seguido de um nome mostra algumas informações de um animal na reserva.\n\
Seguido de + e um número coloca na reserva um animal do comando Animal.\n\
Seguido de - e um nome retira o animal da reserva."
const m_lidar_reserva = "Você não sabe como lidar com animais reserva."
const m_lugar = "Não pode lidar com animais reserva aqui."
const m_batalha = "Você está lutando pela sua vida."
const m_nenhum_reserva = "Não possui nenhum animal reserva."
const m_animais = "Animais reserva"
const m_animal_cheio = "Sem espaço para pegar animal reserva."
const m_reserva_cheio = "Atingido o limite de animais reserva."
const m_sem_animal = "Você não possui $m."
const m_sem_reserva = "Você não possui animal reserva $m."
const m_outra_sala = "Animal $m não está aqui."
const m_guardar = "Você guardou $a N$m."
const m_pegar = "Você pegou $a N$m."
const m_dasala1 = "Você traz $a de volta."
const m_dasala2 = "$P traz $a de volta."
const m_parasala1 = "Você solta animal reserva $a."
const m_parasala2 = "$P solta $a."
const admordem = "m_ajuda m_lidar_reserva m_lugar m_batalha m_nenhum_reserva\n\
m_animais m_animal_cheio m_reserva_cheio m_sem_animal m_sem_reserva\n\
m_outra_sala m_guardar m_pegar m_parasala1 m_parasala2"

func escr
  se arg0.dentro3.total != txt(int(arg0.dentro3.total))
    $mens.mp(m_lidar_reserva, arg0)
  senao !arg0.dono.s_reserva
    $mens.mp(m_lugar, arg0)
  senao arg0.atkenv
    $mens.mp(m_batalha, arg0)
  senao !arg1
    listar(arg0)
  senao txt(arg1, 0, 1) == "+"
    guardar(arg0, txtremove(txt(arg1, 1), "ed"))
  senao txt(arg1, 0, 1) == "-"
    retirar(arg0, txtremove(txt(arg1, 1), "ed"))
  senao
    mostrar(arg0, arg1)
  fimse

func listar # Lista animais na reserva
# arg0 = personagem
  textotxt t
  txt100 lin
  listaitem i
  epara i = arg0.dentro3.ini, i, i.depois
    se i.obj.pvidaok
      lin = "1 " + txtcopiamai(i.obj.nome, "A") + "  N" + i.obj.pnivel
    senao
      lin = "1 (" + txtcopiamai(i.obj.nome, "A") + ")  N" + i.obj.pnivel
    fimse
    t.addfim(lin)
  efim
  se !t.linhas
    $mens.mp(m_nenhum_reserva, arg0)
  senao
    t.juntalin("(", "x)")
    arg0.msg2("\b\c6" + m_animais + ":\b\n" + t.remove(1000))
  fimse

func mostrar # Mostra um animal da reserva
# arg0 = personagem
# arg1 = nome do animal
  $mens.mens = arg1
  ref r = misc:itemlista(arg0, arg1, arg0.dentro3)
  ret !r, $mens.mp(m_sem_reserva, arg0)
  txt100 t1
  arg0.msg("\b\c6Animal reserva \"" + r.nome + "\" nível "+ r.pnivel + "\b")
  t1 = "Vida " + r.pvida + "/" + r.pvidamax
  t1 += "  Mana " + r.pmana + "/" + r.pmanamax
  t1 += "  Vigor " + r.pmove + "/" + r.pmovemax
  arg0.msg(t1)

func guardar # Guarda um animal na reserva
# arg0 = personagem
# arg1 = nome ou número do animal
  ret arg0.dentro3.total >= config:animal2, $mens.mp(m_reserva_cheio, arg0)
  ref r = misc:persodentro(arg0, arg1)
  $mens.mens = arg1
  ret !r, $mens.mp(m_sem_animal, arg0)
  ret r.dono != arg0 && r.dono != arg0.dono, $mens.mp(m_outra_sala, arg0)
  listaobj e
  epara e.addfim(arg0.evento, r.evento, arg0.dono.evento), e, e.ini.remove
    ret e.objini.cmd_reservaguarda(arg0, r, arg0.dono), nulo
  efim
  se r.dono != arg0
    r.mudadono(arg0)
    r.idono.remove, r.idono = r.dono.dentro3.addfim(r)
    r.pnumero = 0
    arg0.recalc = 1
    $mens.mens = r.pnivel
    $mens.p(arg0, r)
    $mens.mvis2(m_dasala1, m_dasala2)
  senao
    r.idono.remove, r.idono = r.dono.dentro3.addfim(r)
    r.pnumero = 0
    arg0.recalc = 1
    $mens.mens = r.pnivel
    $mens.mp(m_guardar, arg0, r)
  fimse
  arg0.persoesc == r && (arg0.persoesc = arg0)

func retirar # Pega um animal da reserva
# arg0 = personagem
# arg1 = nome do animal
  ref r = misc:itemlista(arg0, arg1, arg0.dentro3)
  se !r
    $mens.mens = arg1
    $mens.mp(m_sem_reserva, arg0)
  senao !arg0.animalmais
    $mens.mp(m_animal_cheio, arg0)
  senao int(r.p_animal) != 1
    r.idono.remove, r.idono = r.dono.dentro2.addfim(r)
    r.pnumero = 0
    arg0.acertanumero
    arg0.recalc = 1
    $mens.mens = r.pnivel
    $mens.mp(m_pegar, arg0, r)
  senao
    r.mudadono(arg0.dono)
    r.pnumero = 0
    arg0.acertanumero
    arg0.recalc = 1
    $mens.mens = r.pnivel
    $mens.p(arg0, r)
    $mens.mvis2(m_parasala1, m_parasala2)
  fimse


classe m_cmd_reserva
herda comando_menu_cmd
#
const nome1 = "Animais reserva"
const info1 = "Quantos animais pode guardar com o comando Reserva\n\
Se 0, o comando Reserva deixa de existir"
const tipo1 = "opc_numero--"
const clas1 = "config"
const vari1 = "animal2"
const extr1 = "0 500"
#
const nome2 = "Ajuda"
const tipo2 = "opc_texto"
const clas2 = "cmd_reserva"
const vari2 = "m_ajuda"
#
const nome3 = "Não lida com reserva"
const tipo3 = "opc_linha"
const clas3 = "cmd_reserva"
const vari3 = "m_lidar_reserva"
#
const nome4 = "Lugar"
const tipo4 = "opc_linha"
const clas4 = "cmd_reserva"
const vari4 = "m_lugar"
#
const nome5 = "Durante a batalha"
const tipo5 = "opc_linha"
const clas5 = "cmd_reserva"
const vari5 = "m_batalha"
#
const nome6 = "Sem animal reserva"
const tipo6 = "opc_linha"
const clas6 = "cmd_reserva"
const vari6 = "m_nenhum_reserva"
#
const nome7 = "Lista de animais"
const tipo7 = "opc_linha"
const clas7 = "cmd_reserva"
const vari7 = "m_animais"
#
const nome8 = "Animais cheio"
const tipo8 = "opc_linha"
const clas8 = "cmd_reserva"
const vari8 = "m_animal_cheio"
#
const nome9 = "Reserva cheio"
const tipo9 = "opc_linha"
const clas9 = "cmd_reserva"
const vari9 = "m_reserva_cheio"
#
const nome10 = "Sem animal"
const tipo10 = "opc_linha"
const clas10 = "cmd_reserva"
const vari10 = "m_sem_animal"
#
const nome11 = "Sem reserva"
const tipo11 = "opc_linha"
const clas11 = "cmd_reserva"
const vari11 = "m_sem_reserva"
#
const nome12 = "Em outra sala"
const tipo12 = "opc_linha"
const clas12 = "cmd_reserva"
const vari12 = "m_outra_sala"
#
const nome13 = "Guardar animal"
const tipo13 = "opc_linha"
const clas13 = "cmd_reserva"
const vari13 = "m_guardar"
#
const nome14 = "Pegar animal"
const tipo14 = "opc_linha"
const clas14 = "cmd_reserva"
const vari14 = "m_pegar"
#
const nome15 = "Da sala perso"
const tipo15 = "opc_linha"
const clas15 = "cmd_reserva"
const vari15 = "m_dasala1"
#
const nome16 = "Da sala outros"
const tipo16 = "opc_linha"
const clas16 = "cmd_reserva"
const vari16 = "m_dasala2"
#
const nome17 = "Para sala perso"
const tipo17 = "opc_linha"
const clas17 = "cmd_reserva"
const vari17 = "m_parasala1"
#
const nome18 = "Para sala outros"
const tipo18 = "opc_linha"
const clas18 = "cmd_reserva"
const vari18 = "m_parasala2"
