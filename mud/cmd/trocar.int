classe h_cmd_trocar
herda comando_comum
const classemenu = "m_cmd_trocar"
const objcmd = config:animal1 ? este
const txtajuda = config:animal1 ? txtajuda1(m_ajuda)
#
const m_ajuda = "-t\n\
-s\n\
-s <item>\n\
-s <número> <número>\n\
Sem argumentos pega um item do animal escolhido.\n\
Com o nome de um item, dá um item para o animal escolhido.\n\
Com dois números, troca dois animais de lugar."
const m_batalha = "Você não consegue trocar durante a batalha."
const m_animal_escolha = "Primeiro escolha um animal."
const m_animal_sem = "$A não carrega nenhum item."
const m_perso_cheio = "Você não consegue carregar $o."
const m_naover = "Você não vê $m."
const m_longe = "$A está longe."
const m_animal_cheio = "$A não consegue carregar $o."
const m_pegar1 = "Você pega $o de $a."
const m_pegar2 = "$P pega $o de $a."
const m_pegar_dar1 = "$P pega $n de $b e dá $o."
const m_pegar_dar2 = "$P pega $n de $b e dá $o."
const m_dar1 = "Você dá $o para $a."
const m_dar2 = "$P dá $o para $a."
const m_semtroca = "Nada para ser trocado."
const m_trocar = "Você troca de lugar $1 $o e $2 $n."
const admordem = "m_ajuda m_batalha m_animal_escolha m_animal_sem\n\
m_perso_cheio m_naover m_longe m_animal_cheio m_pegar1 m_pegar2\n\
m_pegar_dar1 m_pegar_dar2 m_dar1 m_dar2 m_semtroca m_trocar"

func escr
  refvar perm_msg = $perm_[arg0.jogperm].item
  ret perm_msg, $mensjog.msg(perm_msg, arg0)
  ret arg0.atkenv, $mens.mp(m_batalha, arg0)
  ret !arg1, escr_pegar(arg0)
  uint8 p1
  uint8 p2
  p1 = txt1(arg1), p2 = txt2(arg1)
  se txt1(arg1) != p1 || !p1 || p1 > config:animal1
    escr_dar(arg0, arg1)
  senao txt2(arg1) != p2 || !p2 || p2 > config:animal1
    escr_dar(arg0, arg1)
  senao
    escr_trocar(arg0, p1, p2)
  fimse

func escr_pegar # Pegar item do animal
# arg0 = personagem
  refvar animal = ref(arg0.persoesc)
  ret animal == arg0, $mens.mp(m_animal_escolha, arg0)
  ret animal.dono != arg0 && animal.dono != arg0.dono, $mens.mp(m_longe, arg0, animal)
  refvar rec = ref(animal.dentro1.ini.obj)
  se !rec
    $mens.mp(m_animal_sem, arg0, animal)
  senao !rec.cabedentro(arg0)
    $mens.mp(m_perso_cheio, arg0, nulo, rec)
  senao
    rec.mudadono(arg0, 1)
    $mens.p(arg0, animal, rec)
    $mens.mvis2(m_pegar1, m_pegar2)
  fimse

func escr_dar # Dar item para o animal
# arg0 = personagem
# arg1 = nome do item
  refvar animal = ref(arg0.persoesc)
  ret animal == arg0, $mens.mp(m_animal_escolha, arg0)
  ret animal.dono != arg0 && animal.dono != arg0.dono, $mens.mp(m_longe, arg0, animal)
  ref env = misc:itemdentro(arg0, arg1)
  ref rec = animal.dentro1.ini.obj
  se !env
    $mens.mens = arg1
    $mens.mp(m_naover, arg0)
  senao env.objnum >> 1 > 1 # Animal não carrega objetos com coisas dentro
    $mens.mp(m_animal_cheio, arg0, animal, env)
  senao !rec # Se animal não está carregando nada
    env.mudadono(animal, 1)
    $mens.p(arg0, animal, env)
    $mens.mvis2(m_dar1, m_dar2)
  senao !rec.cabedentro(env)
    $mens.mp(m_animal_cheio, arg0, animal, env)
  senao
    env.mudadono(animal, 1)
    rec.mudadono(arg0, 1)
    $mens.p(arg0, animal, env, rec)
    $mens.mvis2(m_pegar_dar1, m_pegar_dar2)
  fimse

func escr_trocar # Trocar dois animais de lugar
# arg0 = personagem
# arg1 = número do primeiro animal
# arg2 = número do segundo animal
  refvar a1 = misc:personum(arg0, arg1)
  refvar a2 = misc:personum(arg0, arg2)
  ret a1 == a2, arg0.msg(m_semtroca)
# Faz a troca
  a1.pnumero = arg2
  a2.pnumero = arg1
# Mensagens
  $mens.o_1 = arg1
  $mens.o_2 = arg2
  $mens.mp(m_trocar, arg0, nulo, a1, a2)


classe m_cmd_trocar
herda comando_menu_cmd
#
const nome1 = "Ajuda"
const tipo1 = "opc_texto"
const vari1 = "m_ajuda"
#
const nome2 = "Durante a batalha"
const tipo2 = "opc_linha"
const vari2 = "m_batalha"
#
const nome3 = "Escolher animal"
const tipo3 = "opc_linha"
const vari3 = "m_animal_escolha"
#
const nome4 = "Animal sem item"
const tipo4 = "opc_linha"
const vari4 = "m_animal_sem"
#
const nome5 = "Animal cheio"
const tipo5 = "opc_linha"
const vari5 = "m_perso_cheio"
#
const nome6 = "Não vê item"
const tipo6 = "opc_linha"
const vari6 = "m_naover"
#
const nome7 = "Animal longe"
const tipo7 = "opc_linha"
const vari7 = "m_longe"
#
const nome8 = "Perso cheio"
const tipo8 = "opc_linha"
const vari8 = "m_animal_cheio"
#
const nome9 = "Pegar item: perso"
const tipo9 = "opc_linha"
const vari9 = "m_pegar1"
#
const nome10 = "Pegar item: outros"
const tipo10 = "opc_linha"
const vari10 = "m_pegar2"
#
const nome11 = "Pegar/dar item: perso"
const tipo11 = "opc_linha"
const vari11 = "m_pegar_dar1"
#
const nome12 = "Pegar/dar item: outros"
const tipo12 = "opc_linha"
const vari12 = "m_pegar_dar2"
#
const nome13 = "Dar item: perso"
const tipo13 = "opc_linha"
const vari13 = "m_dar1"
#
const nome14 = "Dar item: outros"
const tipo14 = "opc_linha"
const vari14 = "m_dar2"
#
const nome15 = "Sem troca"
const tipo15 = "opc_linha"
const vari15 = "m_semtroca"
#
const nome16 = "Trocar de lugar"
const tipo16 = "opc_linha"
const vari16 = "m_trocar"
