classe h_cmd_config
herda comando_comum
const objmenu = $m_cmd_config
const objtipo = 2 # Pode renomear
const posic = 0
const ajudaini = "-t\n\
-s +<opção>\n\
-s -<opção>\n\
Altera algumas preferências do seu personagem. Um sinal de + ou de -\n\
seguido de uma opção ativa ou desativa a opção. Para saber as atuais\n\
preferências, tecle apenas CONFIG, sem parâmetros.\n\n\
As opções são:"
#
const m_desc = "Mostra descrições completas (como ao digitar OLHAR)"
const m_saidas = "Mostra as saídas visíveis após a descrição da sala"
const m_cores = "Recebe sequência de cores no padrão ANSI (somente Telnet)"
const m_acento = "Recebe letras com acentuação (codificação ISO8859-1)"
const m_telepatia = "Recebe mensagens com o comando Telepatia"
const m_chat = "Participa do canal de comunicação CHAT"
const m_novatos = "Participa do canal de comunicação NOVATOS"
const m_emocao = "Mostra (e) nas mensagens geradas com esse comando"
const m_seguir = "Se outros personagens podem escolher seguir você"
const m_moedas = "Pega as moedas do corpo do personagem após vencer uma batalha"
const m_prompt = "Ativa o prompt normal"
const m_bprompt = "Ativa o prompt de batalha"
const m_som1 = "Ativa efeitos sonoros via MSP com volume baixo"
const m_som2 = "Ativa efeitos sonoros via MSP com volume médio\n\
Nota 1: Para volume máximo, usar CONFIG +SOM1 +SOM2\n\
Nota 2: É necessário usar um cliente de MUD com suporte a MSP\n\
(MUD Sound Protocol) e baixar e instalar os sons do MUD"
const m_musica1 = "Ativa música via MSP com volume baixo"
const m_musica2 = "Ativa música via MSP com volume médio\n\
Nota: Para volume máximo, usar CONFIG +MUSICA1 +MUSICA2"
const m_dicas = "Mostra dicas aleatórias de tempos em tempos"
const m_imortal = "Não morre em batalha, sempre consegue fugir, enxerga tudo exceto\n\
cego e no escuro, tranca e destranca tudo, atravessa coisas\n\
fechadas, anda sem gastar movimento, não sai do jogo após algum\n\
tempo sem digitar nada"
const m_sala = "Mostra o nome da sala cadastrada antes do título, desde que\n\
a sala esteja visível pelo comando Lsala"
const m_invquem = "Ocultar a presença no comando quem"
const m_invsala = "Invisível a não administradores"
const m_ac = "Participa do canal de comunicação AC (administração)"
const m_admlog = "Canal ADMLOG - é informado sobre mudanças online no MUD"
const m_cmdlog = "Canal CMDLOG - é informado quando alguém usar Cmd, Cmdp,\n\
Cmdj, ApagarSav, MudaNome, Ban ou teleportar outro jogador"
const m_execlog = "Canal EXECLOG - só tem utilidade para programadores\n\
informa quando a variável exec chegar a 0 (bug no programa)"
#
const m_config_invalida = "Opção inexistente: $m"
const m_config_mudou = "Configuração alterada: $m"
const m_parar_seguir = "Você pára de seguir $a."
#
# As próximas constantes são as opções do comando CONFIG
# Cada constante contém o índice da variável de jogconfig
# As variáveis "opc_" contém o int1 de jogconfig que correspondente à opção
const opc_desc = 0
const opc_saidas = 1
const opc_cores = 2
const opc_acento = 3
const opc_chat = comando("h_cmd_chat") ? 4
const opc_seguir = comando("h_cmd_seguir") ? 5
const opc_prompt = comando("h_cmd_prompt") ? 6
const opc_bprompt = comando("h_cmd_bprompt") ? 7
const opc_telepatia = comando("h_cmd_telepatia") ? 8
const opc_emocao = comando("h_cmd_emocao") ? 9
const opc_moedas = 10
const opc_novatos = comando("h_cmd_novatos") ? 11
const opc_dicas = 12
const opc_som1 = 13
const opc_som2 = 14
const opc_execlog = arg0 & 0x80 ? 15
const opc_sala = arg0 & 2 ? 16
const opc_invquem = arg0 & 4 ? 17
const opc_invsala = arg0 & 8 ? 18
const opc_ac = arg0 & 0x10 ? 19
const opc_imortal = arg0 & 1 ? 20
const opc_admlog = arg0 & 0x20 ? 21
const opc_cmdlog = arg0 & 0x40 ? 22
const opc_musica1 = 23
const opc_musica2 = 24
#
# Ordem das opções em AJUDA CONFIG
const opcoes = "desc saidas cores acento telepatia chat novatos emocao seguir moedas prompt \
bprompt som1 som2 musica1 musica2 dicas imortal sala invquem invsala ac admlog cmdlog execlog"
#
const admordem = "ajudaini m_desc m_saidas m_cores m_acento m_telepatia m_chat m_novatos\n\
m_emocao m_seguir m_moedas m_prompt m_bprompt m_som1 m_som2 m_musica1 m_musica2 m_dicas\n\
m_imortal m_sala m_invquem m_invsala m_ac m_admlog m_cmdlog m_execlog m_config_mudou"

func txtajuda
  refvar perm = $perm_[arg0.jogperm].c_config
  txt512 lin
  textotxt t
  t.addfim(ajudaini)
  epara lin = opcoes, lin, lin = txt2(lin)
    refvar nome = txt1(lin)
    continuar !txt(opc_[nome](perm))
    refvar l = "  \c2" + txtmai(nome) + "\b "
    t.addfim(l + txtesp(15 - inttotal(l)) + txttroca(m_[nome], "\n", "\n            "))
  efim
  ret txtajuda1(t.remove(1000))

func escr
  refvar perm = $perm_[arg0.jogperm].c_config
  prog p
  txt512 x
  txt512 y
  int1 opc.32
  opc.bits = arg0.jogconfig.bits
  se !arg1
    epara p.inifunctudo(este, "opc_"), p.lin, p.depois
      refvar atual = [p.texto](perm)
      se !txt(atual)
      senao opc.[atual]
        x += " +" + txtmai(txt(p.texto, 4))
      senao
        y += " -" + txtmin(txt(p.texto, 4))
      fimse
    efim
    arg0.msg("CONFIG" + x + y)
    ret
  fimse
  x = txttroca(arg1, "+", " +")
  x = txttroca(x, "-", " -")
  x = txtremove(x, "EMD")
  txt512 mudou
  enquanto x
    epara p.inifunctudo(este, "opc_" + txt(txt1(x), 1)), p.lin, p.depois
      refvar atual = [p.texto](perm)
      continuar !txt(atual)
      se txt(x, 0, 1) == "+"
        opc.[atual] = 1
        mudou += " +" + txtmai(txt(p.texto, 4))
      senao txt(x, 0, 1) == "-"
        opc.[atual] = 0
        mudou += " -" + txtmin(txt(p.texto, 4))
      senao
        y += " " + txt1(x)
      fimse
      sair
    efim
    !p.lin && (y += " " + txt1(x))
    x = txt2(x)
  efim
  se y
    $mens.mens = txt(y, 1)
    $mens.mp(m_config_invalida, arg0)
    ret
  fimse
# Checa a propriedade SOM
  se arg0.jogconfig.13 != opc.13 || arg0.jogconfig.14 != opc.14
    !opc.13 && !opc.14 && arg0.sock.msgsock("!!SOUND(off)")
  fimse
# Checa a propriedade MUSICA
  se arg0.jogconfig.23 != opc.23 || arg0.jogconfig.24 != opc.24 && arg0.sock.msgsom_t
    se !opc.23 && !opc.24
      arg0.sock.msgsock("!!MUSIC(off)")
      arg0.sock.msgsom_t = ""
    senao
      refvar som = arg0.sock.msgsom_t
      refvar lin = $som_[som].var.som[opc.24][opc.23]
      lin && arg0.sock.msgsock(lin)
    fimse
  fimse
# Checa a propriedade INVIS
  se arg0.jogconfig.18 != opc.18 # Se a propriedade INVIS mudou
    arg0.recalc = 1 # Deverá atualizar bitinv
  fimse
# Checa a propriedade IMORTAL
  se arg0.jogconfig.20 != opc.20 # Se a propriedade IMORTAL mudou
    arg0.recalc = 1 # Deverá atualizar bitver
    arg0.sock.jogconfig.bits = opc.bits # Para função temposair
    arg0.sock.temposair # Atualiza tempo para sair do MUD
  fimse
# Checa a propriedade SEGUIR
  se !opc.5 # Se não pode ser seguido
    listaitem i
    i = arg0.lseguir.ini
    enquanto i
      se i.obj.tseguir == 0 || i.obj.tseguir == 2
        $mens.mp(m_parar_seguir, i.obj, arg0)
        i.removedepois
      senao
        i.depois
      fimse
    efim
  fimse
# Atualiza a configuração
  arg0.jogconfig.bits = opc.bits
  arg0.sock.jogconfig.bits = opc.bits
  $mens.mens = txt(mudou, 1)
  $mens.mp(m_config_mudou, arg0)

func comando # arg0=classe base, retorna objeto que processa o comando
  prog p
  p.iniherdainv(arg0)
  ret $[p.texto].objcmd


classe m_cmd_config
herda comando_menu_cmd
#
const nome1 = "Ajuda"
const tipo1 = "opc_texto"
const vari1 = "ajudaini"
#
const nome2 = "Config Desc"
const tipo2 = "opc_texto"
const vari2 = "m_desc"
#
const nome3 = "Config Saídas"
const tipo3 = "opc_texto"
const vari3 = "m_saidas"
#
const nome4 = "Config Cores"
const tipo4 = "opc_texto"
const vari4 = "m_cores"
#
const nome5 = "Config Acento"
const tipo5 = "opc_texto"
const vari5 = "m_acento"
#
const nome6 = "Config Telepatia"
const tipo6 = "opc_texto"
const vari6 = "m_telepatia"
#
const nome7 = "Config Chat"
const tipo7 = "opc_texto"
const vari7 = "m_chat"
#
const nome8 = "Config Novatos"
const tipo8 = "opc_texto"
const vari8 = "m_novatos"
#
const nome9 = "Config Emoção"
const tipo9 = "opc_texto"
const vari9 = "m_emocao"
#
const nome10 = "Config Seguir"
const tipo10 = "opc_texto"
const vari10 = "m_seguir"
#
const nome11 = "Config Moedas"
const tipo11 = "opc_texto"
const vari11 = "m_moedas"
#
const nome12 = "Config Prompt"
const tipo12 = "opc_texto"
const vari12 = "m_prompt"
#
const nome13 = "Config Bprompt"
const tipo13 = "opc_texto"
const vari13 = "m_bprompt"
#
const nome14 = "Config Som1"
const tipo14 = "opc_texto"
const vari14 = "m_som1"
#
const nome15 = "Config Som2"
const tipo15 = "opc_texto"
const vari15 = "m_som2"
#
const nome16 = "Config Musica1"
const tipo16 = "opc_texto"
const vari16 = "m_musica1"
#
const nome17 = "Config Musica2"
const tipo17 = "opc_texto"
const vari17 = "m_musica2"
#
const nome18 = "Config Dicas"
const tipo18 = "opc_texto"
const vari18 = "m_dicas"
#
const nome19 = "Config Imortal"
const tipo19 = "opc_texto"
const vari19 = "m_imortal"
#
const nome20 = "Sala"
const tipo20 = "opc_texto"
const vari20 = "m_sala"
#
const nome21 = "Config Invquem"
const tipo21 = "opc_texto"
const vari21 = "m_invquem"
#
const nome22 = "Config Invsala"
const tipo22 = "opc_texto"
const vari22 = "m_invsala"
#
const nome23 = "Config AC"
const tipo23 = "opc_texto"
const vari23 = "m_ac"
#
const nome24 = "Config Admlog"
const tipo24 = "opc_texto"
const vari24 = "m_admlog"
#
const nome25 = "Config Cmdlog"
const tipo25 = "opc_texto"
const vari25 = "m_cmdlog"
#
const nome26 = "Config Execlog"
const tipo26 = "opc_texto"
const vari26 = "m_execlog"
#
const nome27 = "Opção inexistente"
const tipo27 = "opc_linha"
const vari27 = "m_config_invalida"
#
const nome28 = "Mudou config"
const tipo28 = "opc_linha"
const vari28 = "m_config_mudou"
#
const nome29 = "Pára de seguir"
const tipo29 = "opc_linha"
const vari29 = "m_parar_seguir"
#
const nome30 = "Dicas"
const info30 = "Uma dica por linha"
const tipo30 = "opc_texto--"
const clas30 = "config_dicas"
const vari30 = "texto"
const exec30 = "miscdicas:atualiza"
#
const nome31 = "Tempo entre dicas"
const info31 = "Em segundos, ou 0 para desativar as dicas"
const tipo31 = "opc_numero--"
const clas31 = "config_dicas"
const vari31 = "tempo"
const exec31 = "miscdicas:atualiza"
