classe cmd_bug
herda comando_comum
const objcmd = config:bugtipo ? este
const objnivel = 4
const txtajuda = config:bugtipo ? txtajuda1 + m_ajuda
const txtajuda1 = "\b\c3Bug\b\n\Sintaxe: BUG <mensagem>\n"
const posic = 0
#
const m_ajuda = "Envia mensagem para os administradores para relatar bugs (erros no jogo,\n\
geralmente erros de programação)"
const m_sem_args = "Tecle a mensagem após BUG."
const m_nivel = "Para usar o comando BUG você precisa estar no nível $m."

func escr
  refvar perm_msg = $perm_[arg0.jogperm].c_bug
  se perm_msg
    $mensjog.msg(perm_msg, arg0)
  senao arg0.pnivel < config:bugnivel
    $mens.mens = config:bugnivel
    $mens.mp(m_nivel, arg0)
  senao !arg1
    $mens.mp(m_sem_args, arg0)
  senao
    $misclog.bug("bug " + arg0.nome + ": " + arg1)
    $mens.mens = arg1
    $mens.mp(config:bug2msg, arg0)
    indiceitem item
    epara item.ini("un "), txt1(item.txt) == "un", item.depois
      continuar !$perm_[item.obj.jogperm].c_rezou || item.obj.perso.var.u_[arg0.nome]
      $mens.msg(config:bug1msg, item.obj.perso)
    efim
  fimse


classe cmd_ideia
herda comando_comum
const objcmd = config:ideiatipo ? este
const objnivel = 4
const txtajuda = config:ideiatipo ? txtajuda1 + m_ajuda
const txtajuda1 = "\b\c3Ideia\b\n\
Sintaxe: IDEIA <mensagem>\n"
const posic = 0
#
const m_ajuda = "Envia mensagem para os administradores sobre ideias para o jogo."
const m_sem_args = "Tecle a mensagem após IDEIA."
const m_nivel = "Para usar o comando IDEIA você precisa estar no nível $m."

func escr
  refvar perm_msg = $perm_[arg0.jogperm].c_ideia
  se perm_msg
    $mensjog.msg(perm_msg, arg0)
  senao arg0.pnivel < config:ideianivel
    $mens.mens = config:ideianivel
    $mens.mp(m_nivel, arg0)
  senao !arg1
    $mens.mp(m_sem_args, arg0)
  senao
    $misclog.ideia("ideia " + arg0.nome + ": " + arg1)
    $mens.p(arg0)
    $mens.mens = arg1
    $mens.msg(config:ideia2msg, arg0)
    indiceitem item
    epara item.ini("un "), txt1(item.txt) == "un", item.depois
      continuar !$perm_[item.obj.jogperm].c_rezou || item.obj.perso.var.u_[arg0.nome]
      $mens.msg(config:ideia1msg, item.obj.perso)
    efim
  fimse
