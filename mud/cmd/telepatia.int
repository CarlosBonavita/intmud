classe cmd_telepatia
herda comando_comum
const posic = 0
const objnivel = 4
const txtajuda = "\b\c3Telepatia\b\n\
Sintaxe: TELEPATIA <personagem> <mensagem>\n\
         =<personagem> <mensagem>\n\
Envia uma mensagem telepática (privativa) a outro jogador, independente\n\
da sala em que ele estiver.\n\
Veja também os comandos CONFIG e IGNORAR."

func escr
  refvar perm = $perm_[arg0.jogperm]
  se perm.c_telepatia
    ret $mensjog.msg(perm.c_telepatia, arg0)
  senao !arg0.jogconfig.8
    ret arg0.msg("Canal Telepatia está desabilitado; para habilitar tecle CONFIG +TELEPATIA")
  senao !arg1
    ret arg0.msg("Escrever para quem?")
  fimse
  indiceitem item
  item.ini("un " + txts(txtnome(txt1(arg1))))
  $mens.p(arg0, item.obj)
  $mens.mens = txt2(arg1)
  refvar perso = item.obj.perso
  se !perso || perso.jogconfig.17 && !(perm.c_config & 4)
    $mens.msg("Jogador inexistente ou ausente: " + txt1(arg1), arg0)
  senao perso == arg0
    $mens.msg("Escrever para você mesm$s?", arg0)
  senao perso.var.u_[arg0.nome]
    $mens.msg("$C está ignorando você.", arg0)
  senao arg0.var.u_[perso.nome]
    $mens.msg("Você está ignorando $C.", arg0)
  senao !perso.jogconfig.8
    $mens.msg("$C desabilitou o canal Telepatia.", arg0)
  senao arg0.jogconfig.17 && !($perm_[perso.jogperm].c_config & 4)
    $mens.msg("Você está invisível para $C.", arg0)
  senao !txt2(arg1)
    $mens.msg("Escrever o quê?", arg0)
  senao
    $misclog.telepatia("tele " + arg0.nome + " para " + item.obj.nome + ": " + txt2(arg1))
    $mens.msg(config:telepatia2, perso)
    $mens.msg(config:telepatia1, arg0)
  fimse
