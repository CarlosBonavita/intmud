classe cmdfim_telepatia
herda comando_comum
const posic = 0
const txtajuda = "\b\c3Telepatia\b\n\
Sintaxe: TELEPATIA <personagem> <mensagem>\n\
         =<personagem> <mensagem>\n\
Envia uma mensagem telepática (privativa) a outro jogador, independente\n\
da sala em que ele estiver.\n\
Veja também os comandos CONFIG e IGNORAR."

func escr
  ret !arg0.jogconfig.8, arg0.msg("Canal Telepatia está desabilitado.")
  ret !arg1, arg0.msg("Escrever para quem?")
  indiceitem item
  item.ini("un " + txts(txtnome(txt1(arg1))))
  $mens.p(arg0, item.obj)
  $mens.mens = txt2(arg1)
  refvar perso = item.obj.perso
  se !perso || perso.jogconfig.17 && !arg0.jogconfig.23
    arg0.msg("Jogador inexistente ou ausente: " + txt1(arg1))
  senao perso == arg0
    arg0.msginv("Escrever para você mesm$s?")
  senao perso.var.u_[arg0.nome]
    arg0.msginv("$C está ignorando você.")
  senao arg0.var.u_[perso.nome]
    arg0.msginv("Você está ignorando $C.")
  senao !perso.jogconfig.8
    arg0.msginv("$C desabilitou o canal Telepatia.")
  senao arg0.jogconfig.17 && !perso.jogconfig.23
    arg0.msginv("Você está invisível para $C.")
  senao !txt2(arg1)
    arg0.msginv("Escrever o quê?")
  senao
    perso.msginv(config:telepatia2)
    arg0.msginv(config:telepatia1)
  fimse
