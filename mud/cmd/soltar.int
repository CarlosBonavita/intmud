classe cmd_soltar
herda comando_comum
const txtajuda = config:animal1 ? ta1 + ta2 + ta3 + ta4 : ta1 + ta3
const ta1 = "\b\c3Soltar\b\n\
Sintaxe: SOLTAR <objeto>\n\
         SOLTAR <quantidade> <objeto>\n\
         SOLTAR <objeto> <container>\n\
         SOLTAR <quantidade> <objeto> <container>\n"
const ta2 = "         SOLTAR <número do animal>\n"
const ta3 = "Solta um objeto no chão ou em um container (uma bolsa, uma estante, etc).\n\
Para soltar tudo (até 10 objetos) tecle TUDO ao invés do objeto."
const ta4 = "\nPermite também soltar algum animal (se desfazer) que você possuir."
const posic = 5
const m_naover = "Você não vê $m"
const m_nao_possui = "Você não possui $m."
const m_soltaranim1 = "Você solta $a."
const m_soltaranim2 = "$P solta $a."
const m_animal_solto = "Já possui um animal solto."
const m_chama_solta = "Não é possível soltar $a."
const m_naosolta = "Você não consegue soltar itens aqui."
const m_semperm_soltar = "Melhor não soltar nada aqui."
const m_nao_container = "$O não é container."
const m_fechado = "$O está fechad$u."
#
const cmdsg = 0 # 0=comando soltar, 1=comando guardar
uint32 total # Quantidade de itens do mesmo tipo
txt20 mens # Parte da mensagem
txt80 mens2 # Outra parte da mensagem
int8 achou # Quantos itens achou
txt100 erro # Mensagem de erro
int8 soltar # Se pode soltar itens

func escr
  ref item # Item aonde está soltando os itens
  ref sala # Aonde soltar os itens
  listaobj l
  refvar perm_msg = $perm_[arg0.jogperm].item
  ret perm_msg, $mensjog.msg(perm_msg, arg0)
  epara l.addfim(arg0.evento), l, l.ini.remove
    ret l.objini.cmd_soltar(arg0, arg1), nulo
  efim
  arg0.p_espera < 3 && (arg0.p_espera = 3)
# Checa soltar animal
  se !cmdsg && arg1 == intpos(arg1) && int(arg1) && config:animal1 >= arg1
    $mens.mens = arg1
    refvar r = arg0.animalnum(arg1)
    ret !r, $mens.mp(m_nao_possui, arg0)
    ret !arg0.animal2mais, $mens.mp(m_animal_solto, arg0)
    ret soltaranimal(arg0, arg0.dono, r)
  fimse
# Obtém o objeto de onde soltar
  misc:separa(arg1) # Separa nome do objeto do nome do alvo
  se !misc:sep2 # Se não tem alvo, o alvo é a sala
    ret cmdsg, arg0.msg("Guardar aonde?")
    item = sala = arg0.dono
    soltar = sala.s_soltar(arg0)
    mens = ""
    se soltar == 1
      ret $mens.mp(m_naosolta, arg0, nulo, item)
    senao sala.var.ini("u_") && !sala.var.u_[arg0.nome]_
      ret $mens.mp(m_semperm_soltar, arg0, nulo, item)
    fimse
  senao
    soltar = 0
    item = misc:itemtudo(arg0, misc:sep2) # Procura o alvo para o personagem arg0
    sala = item.item ? item.i_salapegar
    se !item
      $mens.mens = misc:sep2
      ret $mens.mp(m_naover, arg0)
    senao item.dono.s_sala && item.dono.var.ini("u_") && !item.dono.var.u_[arg0.nome]_
      ret $mens.mp(m_semperm_soltar, arg0, nulo, item)
    senao !item.i_aberto && !sala
      ret $mens.mp(m_nao_container, arg0, nulo, item)
    senao item.i_aberto > 2 && !arg0.j_imortal
      ret $mens.mp(m_fechado, arg0, nulo, item)
    fimse
    !sala && (sala = item)
    mens = " $e $a"
  fimse
# Solta os itens
  nomeobj n # Para reconhecer os itens
  n.ini(misc:sep1, misc:objmax, misc:tudo)
  achou = 0, erro = "Você não vê isso."
  epara l.addfim(arg0.dentro1), l, l.ini.remove
    refvar ll = l.objini
    continuar !ll.visivel(arg0) || !n.nome(ll.var.z_ident ?? ll.ident, ll.objtot)
    total = n # Quantidade de itens
    soltarobj(arg0, item, sala, ll)
    sair achou >= 10
  efim
  ret achou, nulo
# Solta moedas
  se arg0.var.z_moedas_ && sala.visivel(arg0) && n.nome("moedas", arg0.var.z_moedas_)
    ret sala.i_itens, $mens.mp("Não é possível colocar moedas em $o.", arg0, nulo, sala)
    total = n
    ret soltarmoedas(arg0, sala)
  fimse
# Não encontrou o item
  arg0.msg(erro)

func soltaranimal
# arg0 = quem está soltando
# arg1 = sala destino
# arg2 = animal
  ret arg2.p_animal != 2, $mens.mp(m_chama_solta, arg0, arg2)
  listaobj e
  epara e.addfim(arg0.evento), e, e.ini.remove
    ret e.objini.cmd_soltaranim(arg0, arg1, arg2, arg0), nulo
  efim
  epara e.addfim(arg1.evento), e, e.ini.remove
    ret e.objini.cmd_soltaranim(arg0, arg1, arg2, arg1), nulo
  efim
  epara e.addfim(arg2.evento), e, e.ini.remove
    ret e.objini.cmd_soltaranim(arg0, arg1, arg2, arg2), nulo
  efim
  arg2.mudadono(arg1)
  $mens.p(arg0, arg2)
  $mens.mvis2(m_soltaranim1, m_soltaranim2)
  epara e.addfim(arg0.evento), e, e.ini.remove
    e.objini.cmd_soltouanim(arg0, arg1, arg2, arg0)
  efim
  epara e.addfim(arg1.evento), e, e.ini.remove
    e.objini.cmd_soltouanim(arg0, arg1, arg2, arg1)
  efim
  epara e.addfim(arg2.evento), e, e.ini.remove
    e.objini.cmd_soltouanim(arg0, arg1, arg2, arg2)
  efim

func soltarobj
# arg0 = personagem
# arg1 = item ou sala aonde quer soltar (para mensagens)
# arg2 = objeto que representa o conteúdo interno de arg1
# arg3 = item que quer soltar
# total = quantidade de itens
# Faz achou++ se soltou o item
# Coloca mensagem de erro na variável erro se não conseguiu pegar
  se arg3 == arg1 || arg3 == arg2 # Soltar o objeto dentro dele mesmo
    se arg1.msexo2
      erro = "Não é possível colocar " + arg1.descnome2 + " dentro dele mesmo."
    senao
      erro = "Não é possível colocar " + arg1.descnome2 + " dentro dela mesma."
    fimse
    ret
  senao arg2.i_itens && txtproclin(arg1.i_itens, txt(arg3, 2)) < 0
    erro = "Não é possível colocar " + arg3.descnome2 + " em " + arg1.descnome2 + "."
    ret
  senao arg3.cabedentro(arg2) < total
    se !arg1.item
      erro = "Não consegue soltar tanta coisa aqui."
      ret
    fimse
    erro = arg1.descnome2 + "."
    casovar txtmin(txt1(erro))
    casose "o"
    casose "a"
    casose "os"
    casose "as"
      erro = "Não consegue colocar mais nada n" + txtcopiamai(erro, "aa")
      ret
    casofim
    erro = "Não consegue colocar mais nada em " + erro
    ret
  fimse
  achou++
  listaobj e
  epara e.addfim(arg0.evento), e, e.ini.remove
    ret e.objini.cmd_soltarobj(arg0, arg1, arg3, total, arg0), nulo
  efim
  epara e.addfim(arg1.evento), e, e.ini.remove
    ret e.objini.cmd_soltarobj(arg0, arg1, arg3, total, arg1), nulo
  efim
  epara e.addfim(arg3.evento), e, e.ini.remove
    ret e.objini.cmd_soltarobj(arg0, arg1, arg3, total, arg3), nulo
  efim
  $mens.p(arg0, arg1, arg3)
  mens2 = mens ? "$P coloca " : "$P solta "
  mens2 += total <= 1 ? "$o" : txt(total) + "x $o"
  ref r = arg3
  casovar soltar
  casose "2"
    $mens.mvis1(mens2 + mens + ", que afunda em seguida.")
    arg3.apagar(total)
    sair
  casose "3"
    $mens.mvis1(mens2 + mens + ", que cai e some da sua vista.")
    arg3.apagar(total)
    sair
  casose "4"
    $mens.mvis1(mens2 + mens + ", que se desintegra em seguida.")
    arg3.apagar(total)
    sair
  casose
    $mens.mvis1(mens2 + mens + ".")
    r = arg3.mudadono(arg2, total)
  casofim
  epara e.addfim(arg0.evento), e, e.ini.remove
    e.objini.cmd_soltouobj(arg0, arg1, r, total, arg0)
  efim
  epara e.addfim(arg1.evento), e, e.ini.remove
    e.objini.cmd_soltouobj(arg0, arg1, r, total, arg1)
  efim
  epara e.addfim(r.evento), e, e.ini.remove
    e.objini.cmd_soltouobj(arg0, arg1, r, total, r)
  efim
  arg0.p_espera += 2

func soltarmoedas
# arg0=personagem, arg1=aonde soltar, total=quantidade de moedas
  listaobj e
  epara e.addfim(arg0.evento), e, e.ini.remove
    ret e.objini.cmd_soltarmoedas(arg0, arg1, total, arg0), nulo
  efim
  epara e.addfim(arg1.evento), e, e.ini.remove
    ret e.objini.cmd_soltarmoedas(arg0, arg1, total, arg1), nulo
  efim
  ret !total, nulo
  arg0.var.z_moedas_ -= total
  arg0.var.z_moedas_ < 0 && (arg0.var.z_moedas = "")
  arg0.p_espera += 2
  $mens.p(arg0, arg1)
  mens2 = mens ? "$P coloca " : "$P solta "
  se total == 1
    mens2 += "uma moeda"
  senao total == 2
    mens2 += "duas moedas"
  senao
    mens2 += txt(total) + " moedas"
  fimse
  casovar soltar
  casose "2"
    ret $mens.mvis1(mens2 + mens + ", que afunda em seguida.")
  casose "3"
    ret $mens.mvis1(mens2 + mens + ", que cai e some da sua vista.")
    ret
  casose "4"
    ret $mens.mvis1(mens2 + mens + ", que se desintegra em seguida.")
  casose
    $mens.mvis1(mens2 + mens + ".")
    arg1.var.z_moedas_ += total
    arg1.objmudou = 1
  casofim


classe cmd_guardar
herda cmd_soltar
const cmdsg = 1
const txtajuda = "\b\c3Guardar\b\n\
Sintaxe: GUARDAR\n\
         GUARDAR <objeto> <container>\n\
         GUARDAR <quantidade> <objeto> <container>\n\
Coloca um objeto dentro de um container (uma bolsa, uma estante, etc).\n\
Para guardar tudo (até 10 objetos) tecle TUDO ao invés do objeto."
