classe h_cmd_capturar
herda comando_comum
const classemenu = "m_cmd_capturar"
const posic = 5
#
const v_reserva = 1 # Se animais capturados podem ir para a lista de reservas 0=não 1=sim
const m_ajuda = "-t\n\
-s\n\
-s <objeto>\n\
Usa um objeto para tentar capturar o animal que está lutando com você.\n\
Se o nome do objeto for omitido, usa o último que você escolheu.\n\
Nota: a captura pode falhar se o animal achar que não vale a pena entrar\n\
para a sua equipe. Por isso, é sempre bom enfraquecê-lo antes."
const m_sem_item = "Capturar com o quê?"
const m_nao_serve = "$O não serve para capturar animais."
const m_naover_item = "Você não vê $m."
const m_escolhe = "Você escolhe $m ($o) para capturar animais."
const m_sem_animal = "Não está lutando com nenhum animal."
const m_nao_captura = "Não é possível capturar $a."
const m_hab = "Precisa aprender mais $m para capturar $a."
const m_tem_animal = "Precisa guardar $o antes de pegar $a."
const m_sem_espaco = "Sem espaço para guardar $a."
const capturar_nao1 = "Você joga $o para $a, mas $a recusa."
const capturar_nao2 = "$P joga $o para $a, mas $a recusa."
const capturar_sim1 = "Você joga $o para $a."
const capturar_sim2 = "$P joga $o para $a."
const capturar_equipe1 = "$B entrou para a sua equipe."
const capturar_equipe2 = "$B entrou para a equipe de $P."
const capturar_reserva1 = "$B entrou para a sua equipe como reserva."
const capturar_reserva2 = "$B entrou para a equipe de $P."
const admordem = "m_ajuda m_sem_item m_nao_serve m_naover_item\n\
m_escolhe m_sem_animal m_nao_captura m_hab m_sem_espaco\n\
capturar_nao1 capturar_nao2 capturar_sim1 capturar_sim2\n\
capturar_equipe1 capturar_equipe2 capturar_reserva1 capturar_reserva2"

func escr
  refvar perm_msg = $perm_[arg0.jogperm].item
  ret perm_msg, $mensjog.msg(perm_msg, arg0)
  ref r
# Obtém o item
  $mens.mens = arg1
  ret !arg1 && !arg0.var.z_captura, $mens.mp(m_sem_item, arg0)
  ref item = misc:itemdentro(arg0, arg1 ?? arg0.var.z_captura)
  ret !item, $mens.mp(arg1 ? m_naover_item : m_sem_item, arg0)
  ret !item.i_captura, $mens.mp(m_nao_serve, arg0, nulo, item)
  arg1 && (arg0.var.z_captura = arg1)
# Checa se pode capturar
  int8 lugares # 1=guarda no comando Animal, 2=fica na sala
  r = arg0.atkenv.objlista # Alvo do personagem
  se !r
    ret $mens.mp(arg1 ? m_escolhe : m_sem_animal, arg0, nulo, item)
  senao !r.p_captura || r.perso != 2 || arg0.perso != 3
    ret $mens.mp(m_nao_captura, arg0, r)
  senao r.tseguir == 3 || r.persolugar.total
    ret $mens.mp(m_nao_captura, arg0, r)
  senao r.p_domar_aulas && arg0.var.h_[r.p_domar_hab]_ < r.p_domar_aulas
    $mens.mens = r.p_domar_hab
    ret $mens.mp(m_hab, arg0, r)
  senao r.p_animal != 1 && arg0.animalmais # Pode ir para comando Animal
    lugares = 1
  senao r.p_animal != 0 # Pode ficar na sala
    listaitem li
    epara li = arg0.lseguir.ini, li && li.obj.tseguir != 3, li.depois
    efim
    !li && (lugares = 2)
  fimse
  se !lugares && r.p_animal != 1 && v_reserva && arg0.dentro3.total < config:animal2
    lugares = 1
  fimse
  se !lugares
    refvar tem_animal = r.p_animal == 1 || !config:animal1
    ret $mens.mp(tem_animal ? m_tem_animal : m_sem_espaco, arg0, r, li.obj)
  fimse
# Decide se consegue capturar
  real2 valor = r.pvida * 100 / r.pvidamax
  se valor < 10 # 0% a 10% de vida: valor=10
    valor = 10
  senao valor < 46 # 10% a 46% de vida: valor= 10 a 1
    valor = (50 - valor) / 4
  senao
    valor = 1
  fimse
  casovar r.p_captura
  casose "1" # Muito fácil
    valor *= 2
    sair
  casose "2" # Fácil
    valor *= 1.5
    sair
  casose "3" # Normal
    sair
  casose "4" # Difícil
    valor /= 1.5
    sair
  casose "5" # Muito difícil
    valor /= 2
    sair
  casofim
  $mens.p(arg0, r, item)
  se valor * item.i_captura(r) < r.pnivel * rand(5, 20)
    $mens.mvis2(capturar_nao1, capturar_nao2)
    item.apagar(1)
    ret
  fimse
  $mens.mvis2(capturar_sim1, capturar_sim2)
  item.apagar(1)
  lugares == 2 ? domar(arg0, arg0.dono, r) : pegar(arg0, arg0.dono, r)

func pegar # Pega o animal; vai para o comando Animal
# arg0=quem está pegando, arg1=sala origem, arg2=animal
  listaobj e
  epara e.addfim(arg0.evento), e, e.ini.remove
    ret e.objini.cmd_pegaranim(arg0, arg1, arg2, arg0), nulo
  efim
  epara e.addfim(arg1.evento), e, e.ini.remove
    ret e.objini.cmd_pegaranim(arg0, arg1, arg2, arg1), nulo
  efim
  epara e.addfim(arg2.evento), e, e.ini.remove
    ret e.objini.cmd_pegaranim(arg0, arg1, arg2, arg2), nulo
  efim
  refvar estacheio = !arg0.animalmais
  arg2.batalhafim
  arg2.mudadono(arg0) # Muda de dono
  arg2.objsolto.remove(arg2) # Não conta mais na lista de personagens soltos
  arg0.recalc = 1
  $mens.p(arg0, arg2)
  se estacheio
    arg2.idono.remove, arg2.idono = arg2.dono.dentro3.addfim(arg2)
    $mens.mvis2(capturar_reserva1, capturar_reserva2)
  senao
    $mens.mvis2(capturar_equipe1, capturar_equipe2)
  fimse
  epara e.addfim(arg0.evento), e, e.ini.remove
    sair e.objini.cmd_pegouanim(arg0, arg1, arg2, arg0)
  efim
  epara e.addfim(arg1.evento), e, e.ini.remove
    sair e.objini.cmd_pegouanim(arg0, arg1, arg2, arg1)
  efim
  epara e.addfim(arg2.evento), e, e.ini.remove
    sair e.objini.cmd_pegouanim(arg0, arg1, arg2, arg2)
  efim

func domar # Pega o animal; passa a ser do personagem mas fica na sala
# arg0=quem está pegando, arg1=sala origem, arg2=animal
  listaobj e
  epara e.addfim(arg0.evento), e, e.ini.remove
    ret e.objini.cmd_domaranim(arg0, arg1, arg2, arg0), nulo
  efim
  epara e.addfim(arg1.evento), e, e.ini.remove
    ret e.objini.cmd_domaranim(arg0, arg1, arg2, arg1), nulo
  efim
  epara e.addfim(arg2.evento), e, e.ini.remove
    ret e.objini.cmd_domaranim(arg0, arg1, arg2, arg2), nulo
  efim
  arg2.batalhafim
  arg2.objsolto.remove(arg2) # Não conta mais na lista de personagens soltos
  arg2.mudadomador(arg0)
  $mens.p(arg0, arg2)
  $mens.mvis2(capturar_equipe1, capturar_equipe2)
  epara e.addfim(arg0.evento), e, e.ini.remove
    sair e.objini.cmd_domouanim(arg0, arg1, arg2, arg0)
  efim
  epara e.addfim(arg1.evento), e, e.ini.remove
    sair e.objini.cmd_domouanim(arg0, arg1, arg2, arg1)
  efim
  epara e.addfim(arg2.evento), e, e.ini.remove
    sair e.objini.cmd_domouanim(arg0, arg1, arg2, arg2)
  efim


classe m_cmd_capturar
herda comando_menu_cmd
#
const nome1 = "Animais reserva"
const info1 = "Se animais capturados podem ir para a lista de reservas"
const tipo1 = "opc_simnao"
const vari1 = "v_reserva"
#
const nome2 = "Ajuda"
const tipo2 = "opc_texto"
const vari2 = "m_ajuda"
#
const nome3 = "Sem item"
const tipo3 = "opc_linha"
const vari3 = "m_sem_item"
#
const nome4 = "Item não serve"
const tipo4 = "opc_linha"
const vari4 = "m_nao_serve"
#
const nome5 = "Não vê item"
const tipo5 = "opc_linha"
const vari5 = "m_naover_item"
#
const nome6 = "Escolhe item"
const tipo6 = "opc_linha"
const vari6 = "m_escolhe"
#
const nome7 = "Sem animal"
const tipo7 = "opc_linha"
const vari7 = "m_sem_animal"
#
const nome8 = "Impossível capturar"
const tipo8 = "opc_linha"
const vari8 = "m_nao_captura"
#
const nome9 = "Sem conhecimento"
const tipo9 = "opc_linha"
const vari9 = "m_hab"
#
const nome10 = "Sem espaço"
const tipo10 = "opc_linha"
const vari10 = "m_sem_espaco"
#
const nome11 = "Não capturou: perso"
const tipo11 = "opc_linha"
const vari11 = "capturar_nao1"
#
const nome12 = "Não capturou: outros"
const tipo12 = "opc_linha"
const vari12 = "capturar_nao2"
#
const nome13 = "Capturou: perso"
const tipo13 = "opc_linha"
const vari13 = "capturar_sim1"
#
const nome14 = "Capturou: outros"
const tipo14 = "opc_linha"
const vari14 = "capturar_sim2"
#
const nome15 = "Animal equipe: perso"
const tipo15 = "opc_linha"
const vari15 = "capturar_equipe1"
#
const nome16 = "Animal equipe: outros"
const tipo16 = "opc_linha"
const vari16 = "capturar_equipe2"
#
const nome17 = "Animal reserva: perso"
const tipo17 = "opc_linha"
const vari17 = "capturar_reserva1"
#
const nome18 = "Animal reserva: outros"
const tipo18 = "opc_linha"
const vari18 = "capturar_reserva2"
