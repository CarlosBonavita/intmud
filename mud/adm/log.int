classe adm_log
herda comando_adm, comando_comum, comando_editor
const txtajuda = "\b\c3Log\b\n\
Sintaxe: LOG\n\
         LOG <nome do arquivo>\n\
Sem argumentos, mostra os arquivos de log existentes (no diretório log).\n\
Os arquivos modificados mais recentemente aparecem primeiro.\n\
Com um nome de arquivo, abre o arquivo no editor para visualização."

func escr
  se !arg1
    arqdir dir
    txt100 lin
    lin = dir.abrir("log")
    ret lin, arg0.msg("\bErro: " + lin)
    ret !dir.lin, arg0.msg("\bNenhum arquivo de log")
    debug d
    d.exec = 50000
    textotxt t
    epara nulo, dir.lin, dir.depois
      continuar txtfim(dir.texto, 4) != ".log"
      lin = txttroca(dir.texto + "\b", ".log\b", "")
      lin += txtesp(18 - inttotal(lin)) + " " + dir.tamanho("log/" + dir.texto)
      lin += txtesp(27 - inttotal(lin)) + " "
      refvar data = dir.mtempo("log/" + dir.texto)
      lin += txtsub(data, 2, 1) + "/" + txtsub(data, 1, 1)
      t.addfim(txts(data) + " " + lin + " " + txttroca(txtsub(data, 3, 2), " ", ":"))
    efim
    t.ordena
    refvar txtmais = arg0.sock.txtmais
    txtmais.limpar
    txtmais.addfim("\b\c6Arquivo de log Tamanho Data:\b")
    textopos pos
    epara pos = t.fim, pos.linha, txtmais.addfim(txt2(pos.texto))
      pos.antes
    efim
    arg0.sock.txtmostra
  senao !arg0.sock.teditor.ler("log/" + arg1 + ".log")
    arg0.msg("Arquivo de log não existe: " + arg1)
  senao
    arg0.sock.tpasso = arg1
    passo(arg0.sock, "\b")
  fimse

func passo
  casovar txtmin(txt(arg1, 0, 1))
  casose "\b"
    arg0.teditor.bytes == 1 && arg0.teditor.limpar
    arg0.leditor = arg0.teditor.ini
    arg0.leditor1 = arg0.teditor.ini
    arg0.leditor2 = arg0.teditor.fim
    arg0.leditor2.antes
    arg0.passo = este
    arg0.msg("Abrindo no editor " + arg0.tpasso + ", O opções, X salvar, Z sair")
    ret 1
  casose "z" # Sair do editor
    arg0.msg("Fechando editor")
    arg0.passo = ""
    ret 1
  casose "x" # Salvar o texto
    arg0.msg("Não é possível salvar arquivos de log")
    ret 1
  casofim
  ret passoeditor(arg0, arg1, arg0.teditor.bytes, "Arquivo de log " + arg0.tpasso)
