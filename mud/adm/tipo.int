classe adm_tipo
const iniclasse = !$[arg0] && criar(arg0)
const txtajuda = "\b\c3Comandos para tipos de personagens (raças e classes)\b\n\
  \c2LTIPO\b     Lista tipos de personagens\n\
  \c2CTIPO\b     Cria um tipo de personagem\n\
  \c2ETIPO\b     Edita um tipo de personagem\n\
  \c2ATIPO\b     Apaga um tipo de personagem\n\
  \c2TRES\b      Edita comandos do reset de tipo"


classe adm_ltipo
herda comando_adm
const txtajuda = "\b\c3Ltipo\b\n\
Sintaxe: LTIPO [nome]\n\
Lista os tipos de personagens que começam com o nome especificado.\n\
Se o nome for omitido, lista todos os personagens.\n\
Nota: tipos de personagens são raça e classe."

func escr
  prog p
  txt100 lin
  se !p.iniclasse("t_" + arg1)
    ret arg0.msg("\bNenhum tipo encontrado.")
  fimse
  refvar txtmais = arg0.sock.txtmais
  txtmais.limpar
  lin = "Menu raças:\n  "
  epara nulo, p.lin, p.depois
    continuar $[p.texto].menu != 1
    lin += txt(p.texto, 2) + ", " + $[p.texto].nome
    txtmais.addfim([p.texto]:t_reset ? lin + ", tres" : lin), lin = "  "
  efim
  lin = "Menu classes:\n  "
  epara p.iniclasse("t_" + arg1), p.lin, p.depois
    continuar $[p.texto].menu != 2
    lin += txt(p.texto, 2) + ", " + $[p.texto].nome
    txtmais.addfim([p.texto]:t_reset ? lin + ", tres" : lin), lin = "  "
  efim
  lin = "Menu subclasses:\n  "
  epara p.iniclasse("t_" + arg1), p.lin, p.depois
    continuar $[p.texto].menu != 3
    lin += txt(p.texto, 2) + ", " + $[p.texto].nome
    txtmais.addfim([p.texto]:t_reset ? lin + ", tres" : lin), lin = "  "
  efim
  lin = "Em nenhum menu:\n  "
  epara p.iniclasse("t_" + arg1), p.lin, p.depois
    continuar $[p.texto].menu
    lin += txt(p.texto, 2) + ", " + $[p.texto].nome
    txtmais.addfim([p.texto]:t_reset ? lin + ", tres" : lin), lin = "  "
  efim
  arg0.sock.txtmostra


classe adm_ctipo
herda comando_adm
const txtajuda = "\b\c3Ctipo\b\n\
Sintaxe: CTIPO <nome>\n\
Cria um tipo de personagem com o nome especificado.\n\
O nome deve ser composto de letras, mas pode ter espaços no meio.\n\
Nota: tipos de personagens são raça e classe."

func escr
  prog p
# Checa se digitou nome do tipo
  se arg1 == ""
    ret arg0.msg("Tecle o nome do tipo após CTIPO")
  fimse
# Checa se nome válido
  txt80 nome = txte(txtremove(arg1, "EMDSA"))
  se txtproc(txtsepara(txttroca(nome, " ", ""), "O+DOL,DOL+O"), " ") >= 0
    ret arg0.msg("Nome inválido para tipo de personagem: " + nome)
  senao inttotal(nome) > 40
    ret arg0.msg("Nome deve ter até 40 caracteres: " + nome)
  senao p.existe("t_" + nome)
    ret arg0.msg("Já existe o tipo: " + nome)
  fimse
# Cria tipo
  txt200 lin = "t_" + txts(nome) + "\nconfig/tipo\nherda comum_tipo"
  lin += "\nconst nome = \"" + txtcopiamai(txte(nome), "A") + "\""
  p.criar(lin)
  criar("admordena", "config/tipo")
# config:salvar
  arg0.msg("\bTipo criado: " + nome)
  admlog(arg0, "criou tipo " + nome)


classe adm_atipo
herda comando_adm
const txtajuda = "\b\c3Atipo\b\n\
Sintaxe: ATIPO <nome>\n\
Apaga o tipo de personagem com o nome especificado.\n\
Nota: tipos de personagens são raça e classe."

func escr
  prog p
  se arg1 == ""
    arg0.msg("Tecle um nome após ATIPO")
  senao !p.existe("t_" + arg1)
    arg0.msg("Tipo não existe: " + arg1)
  senao
    arg0.sock.tpasso = arg1
    passo(arg0.sock, "")
  fimse

func passo # Confirmar que quer apagar tipo
  arg0.passo = ""
  se arg1 == "n"
    arg0.msg("Cancelado")
  senao arg1 == "s"
    prog p
    p.apagar("t_" + arg0.tpasso)
    config:salvar
    arg0.msg("Tipo apagado: " + arg0.tpasso)
    admlog(arg0, "apagou tipo " + arg0.tpasso)
  senao
    arg0.passo = este
    arg0.msg("Apagar tipo " + arg0.tpasso + "? Tecle S ou N")
  fimse
  ret 1


classe adm_etipo
herda comando_adm
const txtajuda = "\b\c3Etipo\b\n\
Sintaxe: ETIPO <nome>\n\
Edita o tipo de personagem com o nome especificado.\n\
Nota: tipos de personagens são raça e classe."

func escr
  prog p
  se arg1 == ""
    arg0.msg("Tecle o nome do tipo após ETIPO")
  senao !p.existe("t_" + txts(arg1))
    arg0.msg("Tipo não existe: " + arg1)
  senao
    arg0.sock.var.classe1 = "t_" + txts(txtmin(arg1))
    arg0.sock.m_menu("menu_tipo")
  fimse


classe menu_tipo
herda comando_menu
const titulo = "Tipo " + txt(var.classe1, 2)
const colunas = 25
#
const nome1 = "Texto do menu"
const tipo1 = "opc_linha"
const clas1 = var.classe1
const vari1 = "nome"
#
const nome2 = "Texto menu feminino"
const info2 = "Se ausente, assume Texto do menu"
const tipo2 = "opc_linha"
const clas2 = var.classe1
const vari2 = "nome2"
#
const nome3 = "Texto comando Quem"
const tipo3 = "opc_linha"
const clas3 = var.classe1
const vari3 = "nomequem"
#
const nome4 = "Texto Quem feminino"
const info4 = "Se ausente, assume Texto comando Quem"
const tipo4 = "opc_linha"
const clas4 = var.classe1
const vari4 = "nomequem2"
#
const nome5 = "Descrição"
const info5 = "Aparece quando o jogador escolhe raça ou classe"
const tipo5 = "opc_texto"
const clas5 = var.classe1
const vari5 = "desc"
#
const nome6 = "Descrição feminino"
const info6 = "Se ausente, assume Descrição"
const info6 = "Aparece quando o jogador escolhe raça ou classe"
const tipo6 = "opc_texto"
const clas6 = var.classe1
const vari6 = "desc2"
#
const nome7 = "Está no menu"
const tipo7 = "opc_opcao"
const clas7 = var.classe1
const vari7 = "menu"
const extr7 = "Nenhum menu\nRaças\nClasses\nSubclasses"
#
const nome8 = "Somente sexo"
const tipo8 = "opc_opcao"
const clas8 = var.classe1
const vari8 = "sexo"
const extr8 = "Todos\nFeminino\nMasculino"
#
const nome9 = "Somente raças"
const info9 = "Se esse tipo for uma classe ou subclasse que não existe em todas as raças,\n\
escreva aqui em quais raças existe"
const tipo9 = "opc_tipos"
const clas9 = var.classe1
const vari9 = "racas"
#
const nome10 = "Somente classes"
const info10 = "Se esse tipo for uma subclasse que não existe em todas as classes,\n\
escreva aqui em quais classes existe"
const tipo10 = "opc_tipos"
const clas10 = var.classe1
const vari10 = "classes"
#
const nome11 = "Atributos números"
const tipo11 = "opc_atribnum"
const clas11 = var.classe1
const vari11 = "recalc_auto"
const exec11 = "recalc_ajusta"
#
const nome12 = "Atributos sim/não"
const tipo12 = "opc_atribbit"
const clas12 = var.classe1
const vari12 = "recalc_auto"
const exec12 = "recalc_ajusta"
#
const nome13 = "Atributos ataque"
const info13 = "Afetam os danos quando o personagem ataca alguém"
const tipo13 = "opc_atribatk"
const clas13 = var.classe1
const vari13 = "recalc_auto"
const exec13 = "recalc_ajusta"
#
const nome14 = "Atributos defesa"
const info14 = "Afetam os danos quando alguém ataca o personagem"
const tipo14 = "opc_atribdef"
const clas14 = var.classe1
const vari14 = "recalc_auto"
const exec14 = "recalc_ajusta"
#
const nome15 = "Equip posições"
const info15 = "Se não for <nulo>, são as posições aonde pode usar equipamentos"
const tipo15 = "opc_vetorhex"
const clas15 = var.classe1
const vari15 = "equip1"
const extr15 = config:equip_txt
#
const nome16 = "Equip adicionar"
const info16 = "Adicionar essas posições no personagem"
const tipo16 = "opc_vetorhex"
const clas16 = var.classe1
const vari16 = "equip2"
const extr16 = config:equip_txt
#
const nome17 = "Equip remover"
const info17 = "Remover essas posições do personagem"
const tipo17 = "opc_vetorhex"
const clas17 = var.classe1
const vari17 = "equip3"
const extr17 = config:equip_txt
#
const nome18 = "Exp máx fator 0"
const info18 = "A experiência para subir um nível é o resultado de:\n\
fator0 + fator1 * nível + fator2 * nível ao quadrado"
const tipo18 = "opc_numreal--"
const clas18 = var.classe1
const vari18 = "expmax0"
#
const nome19 = "Exp máx fator 1"
const info19 = "A experiência para subir um nível é o resultado de:\n\
fator0 + fator1 * nível + fator2 * nível ao quadrado"
const tipo19 = "opc_numreal--"
const clas19 = var.classe1
const vari19 = "expmax1"
#
const nome20 = "Exp máx fator 2"
const info20 = "A experiência para subir um nível é o resultado de:\n\
fator0 + fator1 * nível + fator2 * nível ao quadrado"
const tipo20 = "opc_numreal--"
const clas20 = var.classe1
const vari20 = "expmax2"
#
const nome21 = "Exp máx cada nível"
const info21 = "Permite definir a experiência para subir um nível em níveis específicos"
const tipo21 = "opc_nivel--"
const clas21 = var.classe1
const vari21 = "expmax_"
#
const titu22 = "* Somente jogadores"
#
const nome22 = "Nível mínimo"
const info22 = "Quando o personagem é criado ou renasce, ele vai para esse nível.\n\
Se houverem níveis diferentes na raça, classe e subclasse, é considerado\n\
somente o maior."
const tipo22 = "opc_numero"
const clas22 = var.classe1
const vari22 = "nmin"
const extr22 = "0 65535"
#
const nome23 = "Nível máximo"
const info23 = "Quando o personagem chega no nível máximo, não sobe mais de nível, mas pode\n\
renascer. Se houverem níveis diferentes na raça, classe e subclasse,\n\
é considerado somente o maior. Se os três forem 0, não renasce."
const tipo23 = "opc_numero"
const clas23 = var.classe1
const vari23 = "nmax"
const extr23 = "0 65535"
#
const nome24 = "Evolui para"
const tipo24 = "opc_tipos"
const info24 = "Os novos tipos que o personagem pode escolher no comando renascer"
const clas24 = var.classe1
const vari24 = "evoluir"
#
const nome25 = "Fome e sede"
const info25 = "Somente raças; se pode sentir fome e sede (se não for imortal)"
const tipo25 = "opc_simnao"
const clas25 = var.classe1
const vari25 = "fome"
#
const nome26 = "Ataque automático"
const info26 = "Habilidade usada em ataques automáticos sem arma"
const tipo26 = "opc_linha"
const clas26 = var.classe1
const vari26 = "atipo"
#
const nome27 = "Volume mínimo"
const info27 = "Somente raças; o volume é escolhido aleatoriamente"
const tipo27 = "opc_numero"
const clas27 = var.classe1
const vari27 = "volmin"
const exec27 = "jogador:ajustajog"
#
const nome28 = "Volume máximo"
const info28 = "Somente raças; o volume é escolhido aleatoriamente"
const tipo28 = "opc_numero"
const clas28 = var.classe1
const vari28 = "volmax"
const exec28 = "jogador:ajustajog"
#
const nome29 = "Peso mínimo"
const info29 = "Somente raças; o peso é escolhido aleatoriamente"
const tipo29 = "opc_numero"
const clas29 = var.classe1
const vari29 = "pesomin"
const exec29 = "jogador:ajustajog"
#
const nome30 = "Peso máximo"
const info30 = "Somente raças; o peso é escolhido aleatoriamente"
const tipo30 = "opc_numero"
const clas30 = var.classe1
const vari30 = "pesomax"
const exec30 = "jogador:ajustajog"
#
const nome50 = "Variáveis"
const tipo50 = "opc_linha"
const clas50 = var.classe1
const vari50 = "inivar"


classe comum_tipo
#
# Ordem que deve organizar as constantes:
const admordem = "nome nome2 nomequem nomequem2 menu desc desc2 sexo racas classes\n\
equip1 equip2 equip3 nmin nmax evoluir fome atipo\n\
volmin volmax pesomin pesomax inivar"
const iniclasse = !$[arg0] && criar(arg0)
const fome = 1

func recalc_ajusta # Ajusta variáveis quando algum atributo foi mudado
  refvar nome = txt2(txte(este))
  prog p
  ref r
  epara r = $jogador, r, r = objdepois(r)
    (r.tipo1 == nome || r.tipo2 == nome || r.tipo3 == nome) && (r.recalc = 1)
  efim
  epara p.iniclasse("p_"), p.lin, p.depois
    epara r = $[p.texto], r, r = objdepois(r)
      (r.tipo1 == nome || r.tipo2 == nome || r.tipo3 == nome) && (r.recalc = 1)
    efim
  efim
